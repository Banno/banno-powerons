[*
**  PowerOn Name:       BANNO.LOAN.PAYOFF.FEES.POW
**
**  Copyright 2022 Jack Henry and Associates
**
**  This Banno subroutine PowerOn is called by the main BANNO.LOAN.PAYOFF.V1.POW PowerOn -- if configured
**  in the CFG letterfile. It can be modified to return custom fee amounts and descriptions to the UX for display.
**  --No actual fees are posted by this PowerOn.  The returned data is for display purposes only.--
**
**  For more information check
**  https://github.com/Banno/banno-powerons
**
**  Banno is not responsible for any modifications to this file
**  made by unauthorized personnel.
**
**  DO NOT MODIFY THIS FILE UNLESS YOU KNOW WHAT YOU'RE DOING!
**
**  Modification History:
**    Ver. 1.0.0  08/03/22 R. Robison - Original Programming
**
**  Data will be passed to the subroutine specfile as follows:
**
**   Account Number --> @ENVPARAMCHAR1 --> PAYOFFACCTNUM
**   Loan ID        --> @ENVPARAMCHAR2 --> PAYOFFSLID
**   Payoff Date    --> @ENVPARAMDATE1 --> PAYOFFDATE
**
**  The subroutine specfile will be expected to return any applicable 
**  fees in @ENVPARAMMONEY1-@ENVPARAMMONEY10 with the associated fee 
**  descriptions in @ENVPARAMCHAR1-@ENVPARAMCHAR10.  Return data:
**
**   Fee Amount 1  --> @ENVPARAMMONEY1  Fee Desc 1  --> @ENVPARAMCHAR1
**   Fee Amount 2  --> @ENVPARAMMONEY2  Fee Desc 2  --> @ENVPARAMCHAR2
**   Fee Amount 3  --> @ENVPARAMMONEY3  Fee Desc 3  --> @ENVPARAMCHAR3
**   Fee Amount 4  --> @ENVPARAMMONEY4  Fee Desc 4  --> @ENVPARAMCHAR4
**   Fee Amount 5  --> @ENVPARAMMONEY5  Fee Desc 5  --> @ENVPARAMCHAR5
**   Fee Amount 6  --> @ENVPARAMMONEY6  Fee Desc 6  --> @ENVPARAMCHAR6
**   Fee Amount 7  --> @ENVPARAMMONEY7  Fee Desc 7  --> @ENVPARAMCHAR7
**   Fee Amount 8  --> @ENVPARAMMONEY8  Fee Desc 8  --> @ENVPARAMCHAR8
**   Fee Amount 9  --> @ENVPARAMMONEY9  Fee Desc 9  --> @ENVPARAMCHAR9
**   Fee Amount 10 --> @ENVPARAMMONEY10 Fee Desc 10 --> @ENVPARAMCHAR10
*]

SUBROUTINE 
DEMAND
STATELESS

TARGET=ACCOUNT

DEFINE
 PAYOFFACCOUNTNUM=CHARACTER(10)
 PAYOFFSLID=CHARACTER(4)
 PAYOFFDATE=DATE
 
 TMPLOOP=NUMBER

 PAYOFFFEEAMT=MONEY ARRAY(10)
 PAYOFFFEEDESC=CHARACTER ARRAY(10)
 PAYOFFFEECOUNT=NUMBER
 PAYOFFFEEERROR=CHARACTER
 PAYOFFFEEMAX=10

 ADDFEEAMT=MONEY
 ADDFEEDESC=CHARACTER
END

SETUP
 PAYOFFACCOUNTNUM=@ENVPARAMCHAR1
 PAYOFFSLID=@ENVPARAMCHAR2
 PAYOFFDATE=@ENVPARAMDATE1

 CALL ENVPARAMSINIT
 CALL PAYOFFFEEINIT
END

PRINT TITLE="BANNO.LOAN.PAYOFF.FEES.POW"

 FOR ACCOUNT PAYOFFACCOUNTNUM
  DO
[*
**  Custom fee logic should be added here.  To add a custom fee for display in the UX,
**  set the ADDFEEAMT and ADDFEEDESC variables and then call the PAYOFFFEEADD procedure.
**  Set the PAYOFFFEEERROR variable to a non-blank value to return an error condtion to
**  the calling PowerOn.
*]

   ADDFEEAMT=$35.00
   ADDFEEDESC="Processing Fee"
   CALL PAYOFFFEEADD 
  END 

 CALL PAYOFFFEERETURN
END

PROCEDURE ENVPARAMSINIT

 @ENVPARAMCHAR1="" 
 @ENVPARAMCHAR2="" 
 @ENVPARAMCHAR3="" 
 @ENVPARAMCHAR4="" 
 @ENVPARAMCHAR5="" 
 @ENVPARAMCHAR6="" 
 @ENVPARAMCHAR7="" 
 @ENVPARAMCHAR8="" 
 @ENVPARAMCHAR9="" 
 @ENVPARAMCHAR10=""
 @ENVPARAMMONEY1=$0.00
 @ENVPARAMMONEY2=$0.00
 @ENVPARAMMONEY3=$0.00
 @ENVPARAMMONEY4=$0.00
 @ENVPARAMMONEY5=$0.00
 @ENVPARAMMONEY6=$0.00
 @ENVPARAMMONEY7=$0.00
 @ENVPARAMMONEY8=$0.00
 @ENVPARAMMONEY9=$0.00
 @ENVPARAMMONEY10=$0.00
END

PROCEDURE PAYOFFFEEINIT

 PAYOFFFEECOUNT=0
 PAYOFFFEEERROR=""
END

PROCEDURE PAYOFFFEEADD

 IF PAYOFFFEECOUNT<PAYOFFFEEMAX THEN
  DO
   PAYOFFFEECOUNT=PAYOFFFEECOUNT+1
   PAYOFFFEEAMT(PAYOFFFEECOUNT)=ADDFEEAMT
   PAYOFFFEEDESC(PAYOFFFEECOUNT)=ADDFEEDESC
  END
END

PROCEDURE PAYOFFFEERETURN

 IF PAYOFFFEEERROR<>"" THEN
  @ENVPARAMERRORTEXT=PAYOFFFEEERROR
 ELSE
  DO

   FOR TMPLOOP=1 TO PAYOFFFEECOUNT
    DO
     IF TMPLOOP=1 THEN 
      DO
       @ENVPARAMMONEY1=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR1=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=2 THEN
      DO
       @ENVPARAMMONEY2=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR2=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=3 THEN
      DO
       @ENVPARAMMONEY3=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR3=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=4 THEN
      DO
       @ENVPARAMMONEY4=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR4=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=5 THEN
      DO
       @ENVPARAMMONEY5=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR5=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=6 THEN
      DO
       @ENVPARAMMONEY6=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR6=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=7 THEN
      DO
       @ENVPARAMMONEY7=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR7=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=8 THEN
      DO
       @ENVPARAMMONEY8=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR8=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=9 THEN
      DO
       @ENVPARAMMONEY9=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR9=PAYOFFFEEDESC(TMPLOOP)
      END
     ELSE IF TMPLOOP=10 THEN
      DO
       @ENVPARAMMONEY10=PAYOFFFEEAMT(TMPLOOP)
       @ENVPARAMCHAR10=PAYOFFFEEDESC(TMPLOOP)
      END
    END
  END 
END
