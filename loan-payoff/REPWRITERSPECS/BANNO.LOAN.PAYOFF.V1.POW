[*
**  PowerOn Name:       BANNO.LOAN.PAYOFF.V1.POW
**  Letterfile Name:    BANNO.LOAN.PAYOFF.V1.CFG
**
**  Copyright 2019 Jack Henry and Associates
**
**  This Banno service PowerOn allows the user to request a loan
**  payoff for an entered date.
**
**  For more information check
**  https://github.com/Banno/banno-powerons
**
**  Banno is not responsible for any modifications to this file
**  made by unauthorized personnel.
**
**  DO NOT MODIFY THIS FILE UNLESS YOU KNOW WHAT YOU'RE DOING!
**
**  06/10/20 - TKainz
**    corrected issue with vehicle collateral, boat collateral
**    display
**  01/07/21 - TKainz
**    Added Newline to end of program to allow for install
*]

SYMCONNECT
STATELESS

TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 #INCLUDE "RB.LISTEXPAND.DEF"

 [LETTERFILE DEF]
 CONFIGFILENAME            = "BANNO.LOAN.PAYOFF.V1.CFG"
 LFNUMBER                  = NUMBER
 LFERROR                   = CHARACTER
 LFLINE                    = CHARACTER
 READCONFIGERROR           = NUMBER
 READCONFIGERRORMSG        = CHARACTER

 TERMS	  	           = CHARACTER     ARRAY(40)
 TERMSMAXLINES	           = 40
 TERMSLINESCOUNT           = NUMBER

 LOANINELIGIBLEREASON      = CHARACTER(60)
 PAYOFFACCOUNTNUM          = CHARACTER(10)
 PAYOFFID                  = CHARACTER
 PAYOFFDATE                = DATE

 PROJECTIONERRORMSG        = CHARACTER
 PAYOFFCALCAMOUNT          = MONEY
 PAYOFFCALCPRINCIPALBAL    = MONEY
 PAYOFFCALCINTERESTTYPE    = CHARACTER
 PAYOFFCALCINTRATE         = RATE
 PAYOFFCALCINTERESTDUE     = MONEY
 PAYOFFCALCDUEDATE         = DATE
 PAYOFFCALCAMTPASTDUE      = MONEY
 PAYOFFCALCPASTDUECOUNT    = NUMBER
 PAYOFFCALCLATECHARGEDUE   = MONEY

 LOANTYPES                 = NUMBER        ARRAY(9999)
 TYPESMAX                  = 9999
 LOANWARNINGS              = NUMBER        ARRAY(999)
 ACCTWARNINGS              = NUMBER        ARRAY(999)
 WARNINGLOOP               = NUMBER
 WARNINGSMAX               = 999
 FUTUREDAYS                = NUMBER

 HOMELOANTYPES             = NUMBER        ARRAY(9999)
 BOATLOANTYPES             = NUMBER        ARRAY(9999)
 BOATLOANTRACKINGTYPE      = NUMBER
 BOATLOANTRACKINGFIELD     = CHARACTER(40)
 BOATUSERCHAR              = CHARACTER
 VEHICLELOANTYPES          = NUMBER        ARRAY(9999)
 VEHICLELOANTRACKINGTYPE   = NUMBER
 SECUREDLOANTYPES          = NUMBER        ARRAY(9999)
 LINELIMIT                 = NUMBER
 COLLATERALFIELDS          = CHARACTER(40) ARRAY(20,2)
 COLLATERALFIELDSMAX       = NUMBER
 COLLATERALFIELDSCOUNT     = NUMBER
 COLLATERALTEXT            = CHARACTER
 CURRENTFIELD              = NUMBER
 COLLATERALFIELDDATA       = CHARACTER
 LINECOUNT                 = NUMBER

 USERNUMBER                = NUMBER
 USERCHAR                  = CHARACTER
 USERDATE                  = DATE
 USERRATE                  = RATE
 USERCODE                  = NUMBER
 USERAMOUNT                = MONEY

 I                         = NUMBER
 J                         = NUMBER
 Q                         = CHARACTER
 TEMPLOOP                  = NUMBER
 TMPCHR                    = CHARACTER
 TEMP                      = CHARACTER

 TRUE                      = 1
 FALSE                     = 0
 FOUND                     = NUMBER
 DATENULL                  = '--/--/--'

 VALIDATEERROR             = NUMBER
 LOANGOOD                  = NUMBER
 ERRORFLAG                 = NUMBER

 INTERESTDESC              = CHARACTER(12) ARRAY(9999)

 GENERICUSERERRORMSG       = "We're sorry we've encountered an error. Please contact your credit union."
 POS                       = NUMBER
END

SETUP
 Q=CTRLCHR(34)

 INTERESTDESC(0)="Daily"
 INTERESTDESC(1)="360 Day"
 INTERESTDESC(2)="365.25 Day"
 INTERESTDESC(3)="Daily Billed"
 INTERESTDESC(4)="Daily Billed"
 INTERESTDESC(5)="Actual/360"
 INTERESTDESC(6)="Daily Billed 360"
 INTERESTDESC(7)="Actual/364"
 INTERESTDESC(8)="Scheduled/364"
END

PRINT TITLE="BANNO.LOAN.PAYOFF.V1.POW"
 CALL INITIALIZEDATA
 CALL READCONFIGFILESETTINGS
 IF READCONFIGERROR=TRUE THEN
  DO
   PRINT "{"
   NEWLINE
   PRINT "  "+Q+"clientErrorMessage"+Q+":"
   NEWLINE
   PRINT Q+GENERICUSERERRORMSG+Q
   NEWLINE
   PRINT ","
   NEWLINE
   PRINT "  "+Q+"loggingErrorMessage"+Q+":"
   NEWLINE
   PRINT Q+READCONFIGERRORMSG+Q+","
   NEWLINE
  END

 ELSE IF READCONFIGERROR=FALSE THEN
  DO
   IF @RGSTATE="GETPRELOADDATA" THEN
    DO
     [GET VALUES FROM SYMX CALL]
     TEMP=@RGUSERCHR1
     PAYOFFACCOUNTNUM=SEGMENT(TEMP,1,10)
     PAYOFFID=SEGMENT(TEMP,12,LENGTH(TEMP))
     WHILE LENGTH(PAYOFFID)<4
      DO
       PAYOFFID="0"+PAYOFFID
      END

     [check to see if loan can get a payoff]
     CALL GETLOANDATA
     IF FOUND=FALSE THEN
      DO
       NEWLINE
       PRINT "LOAN NOT FOUND"
       NEWLINE
      END
     ELSE IF LOANGOOD=TRUE THEN
      DO
       PRINT "{"
       NEWLINE
       PRINT "  "+Q+"clientErrorMessage"+Q+":"+Q+Q+","
       NEWLINE
       PRINT "  "+Q+"loggingErrorMessage"+Q+":"+Q+Q+","
       NEWLINE
       PRINT "  "+Q+"results"+Q+":{"
       NEWLINE
       PRINT "  "+Q+"loanEligible"+Q+":"
       PRINT "true,"
       NEWLINE
       PRINT "  "+Q+"maxDays"+Q+":"
       PRINT FUTUREDAYS
       PRINT ","
       NEWLINE
      END
     ELSE IF LOANGOOD=FALSE THEN
      DO
       PRINT "{"
       NEWLINE
       PRINT "  "+Q+"clientErrorMessage"+Q+":"
       PRINT Q+GENERICUSERERRORMSG+Q
       PRINT ","
       NEWLINE
       PRINT "  "+Q+"loggingErrorMessage"+Q+":"
       PRINT Q+LOANINELIGIBLEREASON+Q+","
       NEWLINE
       PRINT "  "+Q+"results"+Q+":{"
       NEWLINE
       PRINT "  "+Q+"loanEligible"+Q+":"
       PRINT "false,"
       NEWLINE
       PRINT "  "+Q+"maxDays"+Q+":"
       PRINT FUTUREDAYS
       PRINT ","
       NEWLINE
      END
    END  [GETPRELOADDATA]

   ELSE IF @RGSTATE="PERFORMLOANPAYOFFCALC" THEN
    DO
     [GET VALUES FROM SYMX CALL]
     TEMP=@RGUSERCHR1
     PAYOFFACCOUNTNUM=SEGMENT(TEMP,1,10)
     PAYOFFID=SEGMENT(TEMP,12,LENGTH(TEMP))
     WHILE LENGTH(PAYOFFID)<4
      DO
       PAYOFFID="0"+PAYOFFID
      END
     PAYOFFDATE=DATEVALUE(@RGUSERCHR2)

     [check to see if loan can get a payoff]
     CALL GETLOANDATA
     IF LOANGOOD=TRUE THEN
      DO
       CALL CALCULATEPAYOFF
      END
     ELSE IF LOANGOOD=FALSE THEN
      DO
       PRINT "{"
       NEWLINE
       PRINT "  "+Q+"clientErrorMessage"+Q+":"
       PRINT Q+GENERICUSERERRORMSG+Q
       PRINT ","
       NEWLINE
       PRINT "  "+Q+"loggingErrorMessage"+Q+":"
       PRINT Q+LOANINELIGIBLEREASON+Q+","
       NEWLINE
       PRINT "  "+Q+"results"+Q+":{"
       NEWLINE
       PRINT "  "+Q+"loanEligible"+Q+":"
       PRINT "false,"
       NEWLINE
       PRINT "  "+Q+"maxDays"+Q+":"
       PRINT FUTUREDAYS
       PRINT ","
       NEWLINE
      END
    END [PREFORMLOANPAYOFFCALC]
  END [CONFIG_FALSE]

  [PRINT DISCLAIMER FOR BOTH]
  PRINT "  "+Q+"disclaimerText"+Q+":["
  NEWLINE
  I=1
  WHILE I<TERMSMAXLINES AND TERMS(I)<>""
   DO
    IF I>1 THEN
     PRINT ","
    PRINT Q+TERMS(I)+" "+Q
    NEWLINE
    I=I+1
   END
  PRINT "]"
  NEWLINE

  PRINT "}}"
  NEWLINE
END

PROCEDURE INITIALIZEDATA
 FOUND=FALSE
 WHILELIMIT=100000
 FOR I=1 TO TERMSMAXLINES
  DO
   TERMS(I)=""
  END

 [clear variable]
 FOR TEMPLOOP=1 TO WARNINGSMAX
  DO
   LOANWARNINGS(TEMPLOOP)=FALSE
   ACCTWARNINGS(TEMPLOOP)=FALSE
  END

 [clear variable]
 FOR TEMPLOOP=0 TO TYPESMAX
  DO
   LOANTYPES(TEMPLOOP)=TRUE
  END
END

PROCEDURE READCONFIGFILESETTINGS
 READCONFIGERROR=FALSE
 READCONFIGERRORMSG=""

[ Parse letterfile]
 FILEOPEN("LETTER",CONFIGFILENAME,"READ",LFNUMBER,LFERROR)
 IF LFERROR<>"" THEN
  DO
   READCONFIGERROR=TRUE
   READCONFIGERRORMSG="Error Opening Letterfile "+CONFIGFILENAME+": "+LFERROR
  END
 ELSE
  DO
   TERMSLINESCOUNT=0
   WHILE LFERROR=""
    DO
     FILEREADLINE(LFNUMBER,LFLINE,LFERROR)
     IF SEGMENT(LFLINE,1,1)<>"*" AND LFLINE<>"" THEN
      DO
       [ Valid loan types]
       IF SEGMENT(LFLINE,1,3)="LT:" THEN
        DO
         LELISTINPUT=SEGMENT(LFLINE,4,LENGTH(LFLINE))
         CALL LISTEXPAND
         FOR I=0 TO TYPESMAX
          DO
           LOANTYPES(I)=LELIST(I)
          END
        END

      [ Loan payoff max days]
       IF SEGMENT(LFLINE,1,3)="FD:" THEN
        DO
         FUTUREDAYS=VALUE(SEGMENT(LFLINE,4,LENGTH(LFLINE)))
        END

       [ Loan warning codes]
       IF SEGMENT(LFLINE,1,3)="LW:" THEN
        DO
         IF LENGTH(LFLINE)>3 THEN
          DO
           LELISTINPUT=SEGMENT(LFLINE,4,LENGTH(LFLINE))
           CALL LISTEXPAND
           FOR I=1 TO WARNINGSMAX
            DO
             LOANWARNINGS(I)=LELIST(I)
            END
          END
        END

       [ Acct warning codes]
       IF SEGMENT(LFLINE,1,3)="AW:" THEN
        DO
         IF LENGTH(LFLINE)>3 THEN
          DO
           LELISTINPUT=SEGMENT(LFLINE,4,LENGTH(LFLINE))
           CALL LISTEXPAND
           FOR I=1 TO WARNINGSMAX
            DO
             ACCTWARNINGS(I)=LELIST(I)
            END
          END
        END

       [ Valid home loan types]
       IF SEGMENT(LFLINE,1,4)="HLT:" THEN
        DO
         LELISTINPUT=SEGMENT(LFLINE,5,LENGTH(LFLINE))
         CALL LISTEXPAND
         FOR I=0 TO TYPESMAX
          DO
           HOMELOANTYPES(I)=LELIST(I)
          END
        END

       [ Valid boat loan types]
       IF SEGMENT(LFLINE,1,4)="BLT:" THEN
        DO
         LELISTINPUT=SEGMENT(LFLINE,5,LENGTH(LFLINE))
         CALL LISTEXPAND
         FOR I=0 TO TYPESMAX
          DO
           BOATLOANTYPES(I)=LELIST(I)
          END
        END

      [ Boat loan tracking type]
       IF SEGMENT(LFLINE,1,4)="BTT:" THEN
        DO
         BOATLOANTRACKINGTYPE=VALUE(LFLINE)
        END

      [ Boat loan tracking field]
       IF SEGMENT(LFLINE,1,4)="INFO" AND
          BOATLOANTRACKINGFIELD="" THEN
        DO
         POS=CHARACTERSEARCH(LFLINE,":")
         IF POS>=5 THEN
          DO
           COLLATERALTEXT=SEGMENT(LFLINE,POS+1,LENGTH(LFLINE))
           CALL COLLATERALTEXTNLTS
           BOATLOANTRACKINGFIELD=COLLATERALTEXT
          END
        END

       [ Valid vehicle loan types]
       IF SEGMENT(LFLINE,1,4)="VLT:" THEN
        DO
         LELISTINPUT=SEGMENT(LFLINE,5,LENGTH(LFLINE))
         CALL LISTEXPAND
         FOR I=0 TO TYPESMAX
          DO
           VEHICLELOANTYPES(I)=LELIST(I)
          END
        END

      [ Vehicle loan tracking type]
       IF SEGMENT(LFLINE,1,4)="VTT:" THEN
        DO
         VEHICLELOANTRACKINGTYPE=VALUE(SEGMENT(LFLINE,5,LENGTH(LFLINE)))
         COLLATERALFIELDSCOUNT=0
         COLLATERALFIELDSMAX=3
         FILEREADLINE(LFNUMBER,LFLINE,LFERROR)
         WHILE LFERROR="" AND
               SEGMENT(LFLINE,1,1)<>"*" AND
               COLLATERALFIELDSCOUNT<COLLATERALFIELDSMAX
          DO
           IF LFLINE<>"" THEN
            DO
             COLLATERALFIELDSCOUNT=COLLATERALFIELDSCOUNT+1
             POS=CHARACTERSEARCH(LFLINE,":")
             IF POS=0 THEN
              DO
               COLLATERALTEXT=SEGMENT(LFLINE,1,LENGTH(LFLINE))
               CALL COLLATERALTEXTNLTS
               COLLATERALFIELDS(COLLATERALFIELDSCOUNT,2)=COLLATERALTEXT
              END
             ELSE
              DO
               COLLATERALTEXT=SEGMENT(LFLINE,1,POS-1)
               CALL COLLATERALTEXTNLTS
               COLLATERALFIELDS(COLLATERALFIELDSCOUNT,1)=COLLATERALTEXT

               COLLATERALTEXT=SEGMENT(LFLINE,POS+1,LENGTH(LFLINE))
               CALL COLLATERALTEXTNLTS
               COLLATERALFIELDS(COLLATERALFIELDSCOUNT,2)=COLLATERALTEXT
              END
            END
           FILEREADLINE(LFNUMBER,LFLINE,LFERROR)
          END
        END

       [ Valid secured loan types]
       IF SEGMENT(LFLINE,1,4)="SLT:" THEN
        DO
         LELISTINPUT=SEGMENT(LFLINE,5,LENGTH(LFLINE))
         CALL LISTEXPAND
         FOR I=0 TO TYPESMAX
          DO
           SECUREDLOANTYPES(I)=LELIST(I)
          END
        END

       [ Terms & conditions]
       IF SEGMENT(LFLINE,1,3)="TC:" AND TERMSLINESCOUNT<TERMSMAXLINES THEN
        DO
         TERMSLINESCOUNT=TERMSLINESCOUNT+1
         TERMS(TERMSLINESCOUNT)=SEGMENT(LFLINE,4,LENGTH(LFLINE))
        END
      END
    END
  END
END  [READCONFIGFILESETTINGS]


PROCEDURE GETLOANDATA
 LOANGOOD=TRUE
 FOR ACCOUNT WITH NUMBER PAYOFFACCOUNTNUM
  DO
   FOR EACH LOAN WITH (VALUE(LOAN:ID)=VALUE(PAYOFFID))
    DO
     FOUND=TRUE
     [CHECK LOAN TYPE]
     IF LOANTYPES(LOAN:TYPE)=FALSE THEN
      DO
       LOANGOOD=FALSE
       LOANINELIGIBLEREASON="Ineligible Loan Type"
      END

     [CHECK MAX DAYS]
     IF PAYOFFDATE>SYSTEMDATE+FUTUREDAYS THEN
      DO
       LOANGOOD=FALSE
       LOANINELIGIBLEREASON="Payoff Days more than Maximum allowed"
      END

     [CHECK LOAN & ACCOUNT WARNINGS]
     FOR WARNINGLOOP=1 TO WARNINGSMAX
      DO
       IF ANYWARNING(ACCOUNT,WARNINGLOOP) AND
          ACCTWARNINGS(WARNINGLOOP)=TRUE THEN
        DO
         LOANGOOD=FALSE
         LOANINELIGIBLEREASON="Account Warning Found"
        END
       IF ANYWARNING(LOAN,WARNINGLOOP) AND
          LOANWARNINGS(WARNINGLOOP)=TRUE THEN
        DO
         LOANGOOD=FALSE
         LOANINELIGIBLEREASON="Loan Warning Found"
        END
      END
    END [END LOAN LOOP]
  END [FOR ACCOUNT LOOP]
END [GETLOANDATA]

PROCEDURE CALCULATEPAYOFF
 ERRORFLAG=FALSE

 FOR ACCOUNT PAYOFFACCOUNTNUM
  DO
   FOR EACH LOAN WITH (VALUE(LOAN:ID)=VALUE(PAYOFFID))
    DO
     PAYOFFCALCDUEDATE=LOAN:DUEDATE

     IF LOAN:LOANCODE=6 THEN
      PAYOFFCALCINTRATE=LOAN:AVGWEIGHTEDINTRATE
     ELSE
      PAYOFFCALCINTRATE=LOAN:INTERESTRATE
     LOANPROJECTINIT(1,0)
     @LOANPROJECTCALCULATIONTYPE=3
     @LOANPROJECTPAYOFFDATE=PAYOFFDATE

     [Handle Member Birthdate insurance calc]
     @LOANPROJECTFIRSTINSBIRTHDATE=LOAN:FIRSTINSBIRTHDATE
     @LOANPROJECTSECONDINSBIRTHDATE=LOAN:SECONDINSBIRTHDATE
     LOANPROJECTCALC
     IF @LOANPROJECTERROR="" THEN
      DO
       PAYOFFCALCPRINCIPALBAL=@LOANPROJECTLOANAMOUNT
       PAYOFFCALCINTERESTDUE=@LOANPROJECTINTERESTPAYOFF
       PAYOFFCALCAMTPASTDUE=@LOANPROJECTAMOUNTPASTDUE
       PAYOFFCALCPASTDUECOUNT=@LOANPROJECTCOUNTPASTDUE
       PAYOFFCALCLATECHARGEDUE=@LOANPROJECTLATECHGDUE
       PAYOFFCALCAMOUNT=@LOANPROJECTAMOUNTPAYOFF
       IF @LOANPROJECTINTERESTTYPE>9 THEN
        PAYOFFCALCINTERESTTYPE=GETDATACHARACTER(
         GETPARAMINTTYPEDESCRIPTION,@LOANPROJECTINTERESTTYPE)
       ELSE
        PAYOFFCALCINTERESTTYPE=INTERESTDESC(@LOANPROJECTINTERESTTYPE)
       CALL DISPLAYRESULTS
      END
     ELSE IF @LOANPROJECTERROR<>"" THEN
      DO
       PROJECTIONERRORMSG="Loan Projection Error: "+@LOANPROJECTERROR
       PRINT "{"
       NEWLINE
       PRINT "  "+Q+"clientErrorMessage"+Q+":"
       PRINT Q+GENERICUSERERRORMSG+Q
       PRINT ","
       NEWLINE
       PRINT "  "+Q+"loggingErrorMessage"+Q+":"
       PRINT Q+PROJECTIONERRORMSG+Q+","
       NEWLINE
       PRINT "  "+Q+"results"+Q+":{"
       NEWLINE
       PRINT "  "+Q+"loanEligible"+Q+":"
       PRINT "true,"
       NEWLINE
       PRINT "  "+Q+"maxDays"+Q+":"
       PRINT FUTUREDAYS
       PRINT ","
       NEWLINE
      END
    END
  END
END

PROCEDURE DISPLAYRESULTS
 PRINT "{"
 NEWLINE
 PRINT "  "+Q+"clientErrorMessage"+Q+":"+Q+Q+","
 NEWLINE
 PRINT "  "+Q+"loggingErrorMessage"+Q+":"+Q+Q+","
 NEWLINE
 PRINT "  "+Q+"results"+Q+":{"
 NEWLINE

 PRINT "   "+Q+"totalPayoffAmount"+Q+":"+Q
 TMPCHR=FORMAT("#######9.99",PAYOFFCALCAMOUNT)
 CALL REMOVELEADINGSPACES
 PRINT TMPCHR
 PRINT Q+","
 NEWLINE
 PRINT "   "+Q+"payoffDate"+Q+":"+Q
 PRINT PAYOFFDATE
 PRINT Q+","
 NEWLINE

 PRINT "   "+Q+"accountDetails"+Q+":{"
 NEWLINE

 PRINT "    "+Q+"principalBalance"+Q+":"+Q
 TMPCHR=FORMAT("#######9.99",PAYOFFCALCPRINCIPALBAL)
 CALL REMOVELEADINGSPACES
 PRINT TMPCHR
 PRINT Q+","
 NEWLINE

 PRINT "    "+Q+"interestType"+Q+":"+Q
 PRINT PAYOFFCALCINTERESTTYPE
 PRINT Q+","
 NEWLINE

 IF LOAN:LOANCODE<>3 THEN
  DO
   PRINT "    "+Q+"interestRate"+Q+":"+Q
   TMPCHR=FORMAT("##9.999",PAYOFFCALCINTRATE)
   CALL REMOVELEADINGSPACES
   PRINT TMPCHR
   PRINT Q+","
   NEWLINE
  END

 PRINT "    "+Q+"interestDue"+Q+":"+Q
 TMPCHR=FORMAT("#######9.99",PAYOFFCALCINTERESTDUE)
 CALL REMOVELEADINGSPACES
 PRINT TMPCHR
 PRINT Q+","
 NEWLINE

 PRINT "    "+Q+"dueDate"+Q+":"+Q
 PRINT PAYOFFCALCDUEDATE
 PRINT Q+","
 NEWLINE

 PRINT "    "+Q+"amountPastDueByPayoffDate"+Q+":"+Q
 TMPCHR=FORMAT("#######9.99",PAYOFFCALCAMTPASTDUE)
 CALL REMOVELEADINGSPACES
 PRINT TMPCHR
 PRINT Q+","
 NEWLINE

 PRINT "    "+Q+"pastDuePayoffCount"+Q+":"+Q
 TMPCHR=FORMAT("#######9",PAYOFFCALCPASTDUECOUNT)
 CALL REMOVELEADINGSPACES
 PRINT TMPCHR
 PRINT Q+","
 NEWLINE

 PRINT "    "+Q+"lateChargeDue"+Q+":"+Q
 TMPCHR=FORMAT("#######9.99",PAYOFFCALCLATECHARGEDUE)
 CALL REMOVELEADINGSPACES
 PRINT TMPCHR
 PRINT Q
 NEWLINE
 PRINT "},"
 NEWLINE
 CALL GETCOLLATERAL
END

PROCEDURE GETCOLLATERAL

 [HOME LOAN - 1 NAME RECORD MAX]
 IF HOMELOANTYPES(LOAN:TYPE)=TRUE THEN
  DO
   LINELIMIT=0
   PRINT Q+"associatedCollateral"+Q+": {"
   NEWLINE
   PRINT "   "+Q+"label"+Q+":"+Q+"Address"+Q+","
   NEWLINE
   PRINT "   "+Q+"info"+Q+": ["
   IF ANY LOAN NAME WITH (LOAN NAME:TYPE=29) THEN
    DO
     FOR EACH LOAN NAME WITH LOAN NAME:TYPE=29
      DO
       IF LINELIMIT<1 THEN      [UI is set up for 3 lines. this code is ]
        DO                      [for all 3 lines from name record 29]
         LINELIMIT=LINELIMIT+1
         PRINT Q+LOAN NAME:STREET+Q+","
         IF LOAN NAME:EXTRAADDRESS<>"" THEN
          PRINT Q+LOAN NAME:EXTRAADDRESS+Q+","
         PRINT Q+LOAN NAME:CITY+" "
         PRINT LOAN NAME:STATE+" "
         PRINT LOAN NAME:ZIPCODE+Q+"]"
         NEWLINE
         PRINT "},"
         NEWLINE
        END
      END
    END
   ELSE
    DO
     PRINT "]"
     NEWLINE
     PRINT "},"
    END
  END   [HOMES]


 [BOAT - 3 TRACKING RECORD MAX]
 ELSE IF BOATLOANTYPES(LOAN:TYPE)=TRUE THEN
  DO
   PRINT Q+"associatedCollateral"+Q+": {"
   NEWLINE
   PRINT "   "+Q+"label"+Q+":"+Q+"Boat"+Q+","
   NEWLINE
   PRINT "   "+Q+"info"+Q+": ["
   LINECOUNT=0
   IF ANY LOAN TRACKING WITH (LOAN TRACKING:TYPE=BOATLOANTRACKINGTYPE) THEN
    DO
     FOR EACH LOAN TRACKING WITH (LOAN TRACKING:TYPE=BOATLOANTRACKINGTYPE AND
                                 (LOAN TRACKING:EXPIREDATE=DATENULL OR
                                  LOAN TRACKING:EXPIREDATE>SYSTEMDATE))
      DO
       IF LINECOUNT<3 THEN
        DO
         CALL GETBOATFIELDDATA
         LINECOUNT=LINECOUNT+1
         IF LINECOUNT>1 THEN
          DO
           PRINT ","
           NEWLINE
          END
         PRINT Q
         PRINT BOATUSERCHAR+Q
         NEWLINE
        END
      END  [FOR EACH LN TRK]
    END   [if any ln trk]
   NEWLINE
   PRINT "]},"
   NEWLINE
  END [END BOATS]


 [VEHICLE - 1 TRACKING RECORD MAX]
 ELSE IF VEHICLELOANTYPES(LOAN:TYPE)=TRUE THEN
  DO
   LINELIMIT=0
   PRINT "  "+Q+"associatedCollateral"+Q+": {"
   NEWLINE
   PRINT "  "+Q+"label"+Q+":"+Q+"Vehicle"+Q+","
   NEWLINE
   PRINT "  "+Q+"info"+Q+": ["
   IF ANY LOAN TRACKING WITH (LOAN TRACKING:TYPE=VEHICLELOANTRACKINGTYPE) THEN
    DO
     FOR EACH LOAN TRACKING WITH (LOAN TRACKING:TYPE=VEHICLELOANTRACKINGTYPE and
                                 (LOAN TRACKING:EXPIREDATE=DATENULL OR
                                  LOAN TRACKING:EXPIREDATE>SYSTEMDATE))
      DO
       FOR CURRENTFIELD=1 TO COLLATERALFIELDSCOUNT
        DO
         CALL GETCOLLATERALFIELDDATA
         IF COLLATERALFIELDDATA<>"" THEN
          DO
           PRINT Q
           IF COLLATERALFIELDS(CURRENTFIELD,1)<>"" THEN
            PRINT COLLATERALFIELDS(CURRENTFIELD,1)+":"
           PRINT COLLATERALFIELDDATA+Q
           IF CURRENTFIELD<COLLATERALFIELDSCOUNT THEN
            PRINT ","
           NEWLINE
          END
        END
      END  [EACH TRACKING LOOP]
     PRINT "]},"
     NEWLINE
    END   [IF ANY TRACKING LOOP]
   ELSE
    DO
     PRINT "]"
     NEWLINE
     PRINT "},"
    END
  END  [VEHICLE]

 [SECURED LOAN - 3 PLEDGE MAX]
 ELSE IF SECUREDLOANTYPES(LOAN:TYPE)=TRUE THEN
  DO
   LINELIMIT=1
   PRINT Q+"associatedCollateral"+Q+": {"
   NEWLINE
   PRINT "   "+Q+"label"+Q+":"+Q+"Secured against"+Q+","
   NEWLINE
   PRINT "   "+Q+"info"+Q+": ["
   IF ANY LOAN PLEDGE WITH (LOAN PLEDGE:TYPE=0) THEN
    DO
     FOR EACH LOAN PLEDGE WITH LOAN PLEDGE:TYPE=0
      DO
       IF LINELIMIT<4 THEN         [UI does not support more than 3]
        DO
         IF LINELIMIT>1 AND LINELIMIT<4 THEN
          PRINT ","
         PRINT Q+LOAN PLEDGE:ACCOUNTNUMBER+"-S"+LOAN PLEDGE:ID+Q
         LINELIMIT=LINELIMIT+1
        END
      END
     PRINT "]"
     NEWLINE
     PRINT "},"
     NEWLINE
    END
   ELSE
    DO
     PRINT "]"
     NEWLINE
     PRINT "},"
    END
  END   [SECURED LOANS]


 [NO COLLATERAL]
 ELSE
  DO
   PRINT "  "+Q+"associatedCollateral"+Q+": {"
   NEWLINE
   PRINT "  "+Q+"label"+Q+":"+Q+"None"+Q+","
   NEWLINE
   PRINT "  "+Q+"info"+Q+": []"
   PRINT "},"
   NEWLINE
  END

END

PROCEDURE REMOVELEADINGSPACES
 I=1
 WHILE SEGMENT(TMPCHR,I,I)=" " AND
       I<LENGTH(TMPCHR)
  DO
   I=I+1
  END
 TMPCHR=SEGMENT(TMPCHR,I,LENGTH(TMPCHR))
END

PROCEDURE COLLATERALTEXTNLTS
 CALL COLLATERALTEXTNLS
 CALL COLLATERALTEXTNTS
END

PROCEDURE COLLATERALTEXTNLS

 WHILE LENGTH(COLLATERALTEXT)>0 AND SEGMENT(COLLATERALTEXT,1,1)=" "
  DO
   IF LENGTH(COLLATERALTEXT)=1 THEN
    COLLATERALTEXT=""
   ELSE
    COLLATERALTEXT=SEGMENT(COLLATERALTEXT,2,LENGTH(COLLATERALTEXT))
  END
END

PROCEDURE COLLATERALTEXTNTS

 WHILE LENGTH(COLLATERALTEXT)>0 AND
       SEGMENT(COLLATERALTEXT,LENGTH(COLLATERALTEXT),LENGTH(COLLATERALTEXT))=" "
  DO
   IF LENGTH(COLLATERALTEXT)=1 THEN
    COLLATERALTEXT=""
   ELSE
    COLLATERALTEXT=SEGMENT(COLLATERALTEXT,1,LENGTH(COLLATERALTEXT)-1)
  END
END


PROCEDURE GETCOLLATERALFIELDDATA

 COLLATERALFIELDDATA=""

 IF CHARACTERSEARCH(
     COLLATERALFIELDS(CURRENTFIELD,2),"USERNUMBER")>0 THEN
  DO
   IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER1" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER1
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER2" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER2
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER3" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER3
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER4" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER4
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER5" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER5
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER6" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER6
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER7" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER7
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER8" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER8
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER9" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER9
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER10" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER10
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER11" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER11
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER12" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER12
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER13" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER13
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER14" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER14
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER15" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER15
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER16" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER16
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER17" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER17
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER18" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER18
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER19" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER19
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERNUMBER20" THEN
    USERNUMBER=LOAN TRACKING:USERNUMBER20

   COLLATERALFIELDDATA=FORMAT("#########9",USERNUMBER)
  END
 ELSE IF CHARACTERSEARCH(
          COLLATERALFIELDS(CURRENTFIELD,2),"USERCHAR")>0 THEN
  DO
   IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR1" THEN
    USERCHAR=LOAN TRACKING:USERCHAR1
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR2" THEN
    USERCHAR=LOAN TRACKING:USERCHAR2
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR3" THEN
    USERCHAR=LOAN TRACKING:USERCHAR3
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR4" THEN
    USERCHAR=LOAN TRACKING:USERCHAR4
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR5" THEN
    USERCHAR=LOAN TRACKING:USERCHAR5
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR6" THEN
    USERCHAR=LOAN TRACKING:USERCHAR6
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR7" THEN
    USERCHAR=LOAN TRACKING:USERCHAR7
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR8" THEN
    USERCHAR=LOAN TRACKING:USERCHAR8
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR9" THEN
    USERCHAR=LOAN TRACKING:USERCHAR9
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR10" THEN
    USERCHAR=LOAN TRACKING:USERCHAR10
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR11" THEN
    USERCHAR=LOAN TRACKING:USERCHAR11
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR12" THEN
    USERCHAR=LOAN TRACKING:USERCHAR12
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR13" THEN
    USERCHAR=LOAN TRACKING:USERCHAR13
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR14" THEN
    USERCHAR=LOAN TRACKING:USERCHAR14
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR15" THEN
    USERCHAR=LOAN TRACKING:USERCHAR15
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR16" THEN
    USERCHAR=LOAN TRACKING:USERCHAR16
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR17" THEN
    USERCHAR=LOAN TRACKING:USERCHAR17
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR18" THEN
    USERCHAR=LOAN TRACKING:USERCHAR18
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR19" THEN
    USERCHAR=LOAN TRACKING:USERCHAR19
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCHAR20" THEN
    USERCHAR=LOAN TRACKING:USERCHAR20
   COLLATERALFIELDDATA=USERCHAR
  END
 ELSE IF CHARACTERSEARCH(
          COLLATERALFIELDS(CURRENTFIELD,2),"USERAMOUNT")>0 THEN
  DO
   IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT1" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT1
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT2" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT2
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT3" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT3
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT4" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT4
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT5" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT5
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT6" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT6
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT7" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT7
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT8" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT8
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT9" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT9
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT10" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT10
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT11" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT11
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT12" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT12
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT13" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT13
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT14" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT14
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT15" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT15
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT16" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT16
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT17" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT17
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT18" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT18
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT19" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT19
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERAMOUNT20" THEN
    USERAMOUNT=LOAN TRACKING:USERAMOUNT20
   COLLATERALFIELDDATA=FORMAT("#,###,##9.99",USERAMOUNT)
  END
 ELSE IF CHARACTERSEARCH(
          COLLATERALFIELDS(CURRENTFIELD,2),"USERCODE")>0 THEN
  DO
   IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE1" THEN
    USERCODE=LOAN TRACKING:USERCODE1
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE2" THEN
    USERCODE=LOAN TRACKING:USERCODE2
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE3" THEN
    USERCODE=LOAN TRACKING:USERCODE3
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE4" THEN
    USERCODE=LOAN TRACKING:USERCODE4
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE5" THEN
    USERCODE=LOAN TRACKING:USERCODE5
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE6" THEN
    USERCODE=LOAN TRACKING:USERCODE6
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE7" THEN
    USERCODE=LOAN TRACKING:USERCODE7
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE8" THEN
    USERCODE=LOAN TRACKING:USERCODE8
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE9" THEN
    USERCODE=LOAN TRACKING:USERCODE9
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE10" THEN
    USERCODE=LOAN TRACKING:USERCODE10
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE11" THEN
    USERCODE=LOAN TRACKING:USERCODE11
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE12" THEN
    USERCODE=LOAN TRACKING:USERCODE12
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE13" THEN
    USERCODE=LOAN TRACKING:USERCODE13
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE14" THEN
    USERCODE=LOAN TRACKING:USERCODE14
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE15" THEN
    USERCODE=LOAN TRACKING:USERCODE15
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE16" THEN
    USERCODE=LOAN TRACKING:USERCODE16
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE17" THEN
    USERCODE=LOAN TRACKING:USERCODE17
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE18" THEN
    USERCODE=LOAN TRACKING:USERCODE18
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE19" THEN
    USERCODE=LOAN TRACKING:USERCODE19
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERCODE20" THEN
    USERCODE=LOAN TRACKING:USERCODE20
   COLLATERALFIELDDATA=FORMAT("###9",USERCODE)
  END
 ELSE IF CHARACTERSEARCH(
          COLLATERALFIELDS(CURRENTFIELD,2),"USERDATE")>0 THEN
  DO
   IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE1" THEN
    USERDATE=LOAN TRACKING:USERDATE1
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE2" THEN
    USERDATE=LOAN TRACKING:USERDATE2
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE3" THEN
    USERDATE=LOAN TRACKING:USERDATE3
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE4" THEN
    USERDATE=LOAN TRACKING:USERDATE4
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE5" THEN
    USERDATE=LOAN TRACKING:USERDATE5
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE6" THEN
    USERDATE=LOAN TRACKING:USERDATE6
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE7" THEN
    USERDATE=LOAN TRACKING:USERDATE7
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE8" THEN
    USERDATE=LOAN TRACKING:USERDATE8
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE9" THEN
    USERDATE=LOAN TRACKING:USERDATE9
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE10" THEN
    USERDATE=LOAN TRACKING:USERDATE10
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE11" THEN
    USERDATE=LOAN TRACKING:USERDATE11
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE12" THEN
    USERDATE=LOAN TRACKING:USERDATE12
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE13" THEN
    USERDATE=LOAN TRACKING:USERDATE13
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE14" THEN
    USERDATE=LOAN TRACKING:USERDATE14
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE15" THEN
    USERDATE=LOAN TRACKING:USERDATE15
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE16" THEN
    USERDATE=LOAN TRACKING:USERDATE16
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE17" THEN
    USERDATE=LOAN TRACKING:USERDATE17
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE18" THEN
    USERDATE=LOAN TRACKING:USERDATE18
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE19" THEN
    USERDATE=LOAN TRACKING:USERDATE19
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERDATE20" THEN
    USERDATE=LOAN TRACKING:USERDATE20
   COLLATERALFIELDDATA=FORMAT("--/--/--",USERDATE)
  END
 ELSE IF CHARACTERSEARCH(
          COLLATERALFIELDS(CURRENTFIELD,2),"USERRATE")>0 THEN
  DO
   IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE1" THEN
    USERRATE=LOAN TRACKING:USERRATE1
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE2" THEN
    USERRATE=LOAN TRACKING:USERRATE2
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE3" THEN
    USERRATE=LOAN TRACKING:USERRATE3
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE4" THEN
    USERRATE=LOAN TRACKING:USERRATE4
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE5" THEN
    USERRATE=LOAN TRACKING:USERRATE5
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE6" THEN
    USERRATE=LOAN TRACKING:USERRATE6
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE7" THEN
    USERRATE=LOAN TRACKING:USERRATE7
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE8" THEN
    USERRATE=LOAN TRACKING:USERRATE8
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE9" THEN
    USERRATE=LOAN TRACKING:USERRATE9
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE10" THEN
    USERRATE=LOAN TRACKING:USERRATE10
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE11" THEN
    USERRATE=LOAN TRACKING:USERRATE11
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE12" THEN
    USERRATE=LOAN TRACKING:USERRATE12
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE13" THEN
    USERRATE=LOAN TRACKING:USERRATE13
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE14" THEN
    USERRATE=LOAN TRACKING:USERRATE14
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE15" THEN
    USERRATE=LOAN TRACKING:USERRATE15
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE16" THEN
    USERRATE=LOAN TRACKING:USERRATE16
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE17" THEN
    USERRATE=LOAN TRACKING:USERRATE17
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE18" THEN
    USERRATE=LOAN TRACKING:USERRATE18
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE19" THEN
    USERRATE=LOAN TRACKING:USERRATE19
   ELSE IF COLLATERALFIELDS(CURRENTFIELD,2)="USERRATE20" THEN
    USERRATE=LOAN TRACKING:USERRATE20
   COLLATERALFIELDDATA=FORMAT("##9.999%",USERRATE)
  END
END

PROCEDURE GETBOATFIELDDATA
 IF CHARACTERSEARCH(BOATLOANTRACKINGFIELD,"USERCHAR")>0 THEN
  DO
   IF BOATLOANTRACKINGFIELD="USERCHAR1" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR1
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR2" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR2
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR3" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR3
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR4" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR4
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR5" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR5
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR6" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR6
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR7" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR7
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR8" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR8
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR9" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR9
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR10" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR10
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR11" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR11
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR12" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR12
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR13" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR13
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR14" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR14
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR15" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR15
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR16" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR16
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR17" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR17
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR18" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR18
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR19" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR19
   ELSE IF BOATLOANTRACKINGFIELD="USERCHAR20" THEN
    BOATUSERCHAR=LOAN TRACKING:USERCHAR20
   COLLATERALFIELDDATA=USERCHAR
  END

END

#INCLUDE "RB.LISTEXPAND"
