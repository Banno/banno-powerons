[*
**  PowerOn Name:       BANNO.MICRO.BATCH.FM.V1.POW
**
**  Copyright 2021 Jack Henry and Associates
**
**  This Banno PowerOn is used in Episys via Batch Control.
**  It will produce a report of EFT records to delete.
**  The CU must run a Miscellaneous Process to actually FM
**  the account. 
**  The user enters the micro account number and the number
**  of days to keep. It selects the EFT record as long as 
**  it is a single frequency, is expired and was processed. 
**  
**  The CU must run a Miscellaneous Process to actually FM
**  the account.
**
**  Any assistance with using this PowerOn in Episys will  
**  be provided by Symitar support.
**
**  For more information check
**  https://github.com/Banno/banno-powerons
**
**  Banno is not responsible for any modifications to this file
**  made by unauthorized personnel.
**
**  Modification History:
**    Ver. 1.0.0  04/15/2021 CM Simms - Original Programming
**
**  DO NOT MODIFY THIS FILE UNLESS YOU KNOW WHAT YOU'RE DOING!
*]
 
TARGET=EFT 

DEFINE
 DAYSTOKEEP=NUMBER
 MICROACCOUNT=CHARACTER(10)
 COUNT=NUMBER
 TEMPCHAR=CHARACTER
 MAXCHARS=10
END

SETUP
 DAYSTOKEEP=NUMBERREAD("ENTER HOW MANY DAYS TO KEEP")
 TEMPCHAR=CHARACTERREAD("ENTER THE MICRO ACCOUNT NUMBER")

 CALL ADDLEADINGZEROS
 MICROACCOUNT=TEMPCHAR
END

SELECT
 ACCOUNT:NUMBER=MICROACCOUNT AND
 EFT:TYPE=3 AND
 EFT:GROUPNUMBER="000000" AND
 EFT:FREQUENCY=0 AND
 EFT:EXPIRATIONDATE<>'--/--/--' AND
 EFT:EXPIRATIONDATE<SYSTEMDATE AND
 EFT:EXPIRATIONDATE<SYSTEMDATE-DAYSTOKEEP AND
 EFT:LASTTRANDATE<>'--/--/--'
END
 
PRINT TITLE="BANNO.MICRO.BATCH.FM.V1.POW"
 HEADER=""
 COL=001 "ACCOUNT "+ACCOUNT:NUMBER+" DELETE EFT LOC "
 PRINT EFT:LOCATOR
 NEWLINE
END

PROCEDURE ADDLEADINGZEROS  
  IF TEMPCHAR <> "" THEN
    WHILE LENGTH(TEMPCHAR)<MAXCHARS
     DO
      TEMPCHAR="0"+TEMPCHAR
     END  
END[PROCEDURE ADDLEADINGZEROS]
