[ BNO.INTERACTIVE.PRO
|
|  Modified By    : TKainz
|  Modified Date  : April 21, 2021
|
|  Modified By    : TKainz
|  Modified Date  : Nov. 06, 2023
|  Modification   : Corrected issue where cross-account loans were being
|                   allowed when only shares were requested.
|
]


PROCEDURE NTBUILDSHARELOANLIST

 IF NTSLTYPE=NTSLTYPESHARE OR NTSLTYPE=NTSLTYPESHARELOAN THEN
  CALL NTBUILDSHARELIST
 IF NTSLTYPE=NTSLTYPELOAN OR NTSLTYPE=NTSLTYPESHARELOAN THEN
  CALL NTBUILDLOANLIST
 NTSHARELOANCOUNT=NTSHARECOUNT+NTLOANCOUNT

 BNODEBUGMSG=FORMAT("** NTSLTYPE:99:",NTSLTYPE)
 CALL BNODEBUGMSGADD
END

PROCEDURE NTBUILDSHARELIST

 NTSHAREMAXSORT=0
 CALL NTSETSERVICELISTS
[
 CALL NTSETEXCLUDESHARELIST
 CALL NTSETCLOSEDSHARELIST
]

 NTSHARECOUNT=0
 NTTOTALSHAREBALANCE=$0.00
 FOR EACH SHARE WITH (SHARE:CHARGEOFFDATE=NTDATENULL AND
                      NTSLCODE(SHARE:SHARECODE)=TRUE)
  DO
   NTCROSSACCTFLAG=FALSE
   NTACCESSIQFLAG=TRUE
   NTACCESSXFEROUTFLAG=TRUE
   NTACCESSXFERINFLAG=TRUE
   NTACCESSWDFLAG=TRUE
   CALL NTADDTOSHARELIST
  END
 UNTIL NTSHARECOUNT>=NTSHAREARRAYMAX

 IF NTCROSSACCTACCESS=TRUE THEN
  DO
   NTCROSSACCTSLTYPE=NTSLTYPESHARE
   CALL NTADDCROSSACCOUNTS
  END

 CALL NTSORTXACCOUNTSSHARE
END

PROCEDURE NTADDTOSHARELIST

 CALL NTCHECKUSERHBENABLE

[Check service codes]
 NTSERVICEXFERINFLAG=FALSE
 NTSERVICEXFEROUTFLAG=FALSE
 NTSERVICEWDFLAG=FALSE
 IF  NTSERVICEXFERINALLFLAG=TRUE THEN
  NTSERVICEXFERINFLAG=TRUE
 ELSE IF NTSERVICEXFEROUTALLFLAG=TRUE THEN
  NTSERVICEXFEROUTFLAG=TRUE
 ELSE IF NTSERVICEWDALLFLAG=TRUE THEN
  NTSERVICEWDFLAG=TRUE

 NTI=1
 WHILE (NTSERVICEXFERINFLAG=FALSE OR
        NTSERVICEXFEROUTFLAG=FALSE OR
	NTSERVICEWDFLAG=FALSE) AND
        NTI<=99
  DO
   IF NTSERVICEXFERINLIST(NTI)=TRUE THEN
    DO
     IF ANYSERVICE(SHARE,NTI) THEN
      NTSERVICEXFERINFLAG=TRUE
    END
   IF NTSERVICEXFEROUTLIST(NTI)=TRUE THEN
    DO
     IF ANYSERVICE(SHARE,NTI) THEN
      NTSERVICEXFEROUTFLAG=TRUE
    END
   IF NTSERVICEWDLIST(NTI)=TRUE THEN
    DO
     IF ANYSERVICE(SHARE,NTI) THEN
      NTSERVICEWDFLAG=TRUE
    END
   NTI=NTI+1
  END

 IF NTSERVICEXFERINFLAG=FALSE THEN
  NTACCESSXFERINFLAG=FALSE
 IF NTSERVICEXFEROUTFLAG=FALSE THEN
  NTACCESSXFEROUTFLAG=FALSE
 IF NTSERVICEWDFLAG=FALSE THEN
  NTACCESSWDFLAG=FALSE

 NTACCESSFLAG=FALSE
 IF NTACCESSTYPE=NTACCESSTYPEIQ AND
    NTACCESSIQFLAG=TRUE THEN
  NTACCESSFLAG=TRUE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEXFERIN AND
         NTACCESSXFERINFLAG=TRUE THEN
  NTACCESSFLAG=TRUE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEXFEROUT AND
         NTACCESSXFEROUTFLAG=TRUE THEN
  NTACCESSFLAG=TRUE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEWD AND
         NTACCESSWDFLAG=TRUE THEN
  NTACCESSFLAG=TRUE

[*  Now remove access based on restictions
*]
 IF SHARE:AVAILABLEBALANCE<=$0.00 AND
   (NTACCESSTYPE=NTACCESSTYPEXFEROUT OR
    NTACCESSTYPE=NTACCESSTYPEWD) THEN
  NTACCESSFLAG=FALSE

 IF NTACCESSTYPE=NTACCESSTYPEIQ AND
    NTCROSSACCTFLAG=TRUE AND
    JHAINFOCROSSACCTIQFLAG=FALSE THEN
  NTACCESSFLAG=FALSE

 IF NTACCESSTYPE<>NTACCESSTYPEIQ AND
    SHARE:CLOSEDATE<>NTDATENULL THEN
  NTACCESSFLAG=FALSE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEIQ THEN
  DO
   NTCLOSEDAVAILFLAG=FALSE
   IF SHARE:CLOSEDATE=NTDATENULL OR
      SHARE:CLOSEDATE>SYSTEMDATE THEN
    NTCLOSEDAVAILFLAG=TRUE
   ELSE
    DO
     NTI=1
     NTDONE=FALSE
     WHILE NTI<=JHAINFOCLOSEDSLLISTMAX AND NTDONE=FALSE
      DO
       IF NTCLOSEDSHARETYPEALLFLAG(NTI)=TRUE OR
         (NTCLOSEDSHARETYPENONEFLAG(NTI)=FALSE AND
          NTCLOSEDSHARETYPELIST(NTI,SHARE:TYPE)=TRUE) THEN
        DO
         NTDONE=TRUE
         IF JHAINFOCLOSEDSHAREAVAILDAYS(NTI)=9999 OR
           (JHAINFOCLOSEDSHAREAVAILDAYS(NTI)<>0 AND
            SYSTEMDATE-SHARE:CLOSEDATE<=
	    JHAINFOCLOSEDSHAREAVAILDAYS(NTI)) THEN
          NTCLOSEDAVAILFLAG=TRUE
        END
       NTI=NTI+1
      END
    END

   IF NTCLOSEDAVAILFLAG=FALSE THEN
    NTACCESSFLAG=FALSE
  END

[*  This block of code just checks for duplicates since ACCESS records
**  can point to duplicate shares/loans.
*]
 IF NTCROSSACCTFLAG=FALSE THEN
  NTSLIDFOUND=FALSE
 ELSE
  DO
   NTFINDID=SHARE:ID
   NTFINDACCTNUM=ACCOUNT:NUMBER
   CALL NTFINDSHAREACCT
  END

 IF NTACCESSFLAG=TRUE AND NTSLIDFOUND=FALSE THEN
  DO
   NTSHARECOUNT=NTSHARECOUNT+1
   NTTOTALSHAREBALANCE=NTTOTALSHAREBALANCE+SHARE:BALANCE
   NTSHAREIQFLAG(NTSHARECOUNT)=NTACCESSIQFLAG
   NTSHAREXFERINFLAG(NTSHARECOUNT)=NTACCESSXFERINFLAG
   NTSHAREXFEROUTFLAG(NTSHARECOUNT)=NTACCESSXFEROUTFLAG
   NTSHAREWDFLAG(NTSHARECOUNT)=NTACCESSWDFLAG
   NTSHAREACCTNUM(NTSHARECOUNT)=ACCOUNT:NUMBER
   NTSHAREID(NTSHARECOUNT)=SHARE:ID
   NTSHARETYPE(NTSHARECOUNT)=SHARE:TYPE
   NTSHAREDESC(NTSHARECOUNT)=SHARE:DESCRIPTION
   NTSHAREAVAILBAL(NTSHARECOUNT)=SHARE:AVAILABLEBALANCE
   NTSHARESORTORDER(NTSHARECOUNT)=0
   IF NTAVAILCALCMETHOD=1 THEN
    NTSHAREAVAILBAL(NTSHARECOUNT)=
     NTSHAREAVAILBAL(NTSHARECOUNT)-SHARE:LOANPAYMENTHOLD
   NTSHAREBALANCE(NTSHARECOUNT)=SHARE:BALANCE
   NTSHARECODE(NTSHARECOUNT)=SHARE:SHARECODE
   NTSHAREMATURITYDATE(NTSHARECOUNT)=SHARE:MATURITYDATE
   CALL NTGETSHAREDISPLAYNAME
   NTSHAREDISPLAYNAME(NTSHARECOUNT)=NTDISPLAYNAME
   NTSHAREDISPLAYNAMEBAL(NTSHARECOUNT)=NTDISPLAYNAMEBAL
   NTSHAREACCOUNTNAME(NTSHARECOUNT)=NAME:LONGNAME
   NTSHARENICKNAME(NTSHARECOUNT)=SHARE:NICKNAME
  END
END

PROCEDURE NTGETSHAREDISPLAYNAME

 IF SHARE:NICKNAME<>"" AND NTCROSSACCTFLAG=FALSE THEN
  NTDISPLAYNAME=SHARE:NICKNAME
 ELSE
  DO
   IF NTCROSSACCTFLAG=TRUE AND NTNAMEINCROSSACCTDISPLAY=TRUE THEN
    NTDISPLAYNAME=NAME:SHORTNAME+" "
   ELSE
    NTDISPLAYNAME=""
   NTACCTNUM=ACCOUNT:NUMBER
   CALL NTGETACCTNUMMASKED
   NTDISPLAYNAME=NTDISPLAYNAME+NTACCTNUMMASKED+" S"+SHARE:ID
  END
 NTDISPLAYNAMEBAL=NTDISPLAYNAME+
  " (Avail: "+FORMAT("###,###,##9.99",SHARE:AVAILABLEBALANCE)+")"
END

PROCEDURE NTADDCROSSACCOUNTS

 FOR EACH PREFERENCE WITH (PREFERENCE:LOCATOR=@PREFLOCATOR)
  DO
   FOR EACH PREFERENCE ACCESS WITH
                    ((PREFERENCE ACCESS:ACCESSTYPE=
		      NTACCESSRECTYPEXFEROUT OR
                      PREFERENCE ACCESS:ACCESSTYPE=
		      NTACCESSRECTYPEALTACCT) AND
                     (PREFERENCE ACCESS:IDTYPE=2 OR
		    ((PREFERENCE ACCESS:IDTYPE=0 AND
	              NTCROSSACCTSLTYPE=NTSLTYPESHARE) OR
		     (PREFERENCE ACCESS:IDTYPE=1 AND
		      NTCROSSACCTSLTYPE=NTSLTYPELOAN))))
    DO
     NTACCESSRECIQFLAG=FALSE
     NTACCESSRECXFEROUTFLAG=FALSE
     NTACCESSRECXFERINFLAG=FALSE
     NTACCESSRECWDFLAG=FALSE

     IF PREFERENCE ACCESS:ACCESSTYPE=NTACCESSRECTYPEXFEROUT THEN
      NTACCESSRECXFERINFLAG=TRUE
     ELSE IF PREFERENCE ACCESS:ACCESSTYPE=NTACCESSRECTYPEALTACCT THEN
      DO
       IF PREFERENCE ACCESS:ENABLEINQUIRY=TRUE THEN
        NTACCESSRECIQFLAG=TRUE
       IF PREFERENCE ACCESS:ENABLEDEPOSIT=TRUE THEN
        NTACCESSRECXFERINFLAG=TRUE
       IF PREFERENCE ACCESS:ENABLEWITHDRAWAL=TRUE THEN
        DO
	 NTACCESSRECXFEROUTFLAG=TRUE
         NTACCESSRECWDFLAG=TRUE
        END
      END

     NTPREFACCESSIDTYPE=PREFERENCE ACCESS:IDTYPE
     NTPREFACCESSID=PREFERENCE ACCESS:ID
     FOR ACCOUNT PREFERENCE ACCESS:ACCOUNTNUMBER
      DO
       BNODEBUGMSG=FORMAT("** NTSLTYPE:99:",NTSLTYPE)
       CALL BNODEBUGMSGADD
       IF (NTPREFACCESSIDTYPE=2 OR NTPREFACCESSIDTYPE=0) AND
          (NTCROSSACCTSLTYPE=NTSLTYPESHARE OR
           NTCROSSACCTSLTYPE=NTSLTYPESHARELOAN) THEN
        DO
         FOR EACH SHARE WITH (SHARE:CHARGEOFFDATE=NTDATENULL AND
                              NTSLCODE(SHARE:SHARECODE)=TRUE AND
                              NTEXCLUDESHARETYPEALLFLAG=FALSE AND
                              NTEXCLUDESHARETYPELIST(SHARE:TYPE)=FALSE)
          DO
           IF NTPREFACCESSIDTYPE=2 OR
             (NTPREFACCESSIDTYPE=0 AND NTPREFACCESSID=SHARE:ID) THEN
            DO
             NTCROSSACCTFLAG=TRUE
             NTACCESSIQFLAG=NTACCESSRECIQFLAG
             NTACCESSXFEROUTFLAG=NTACCESSRECXFEROUTFLAG
             NTACCESSXFERINFLAG=NTACCESSRECXFERINFLAG
             NTACCESSWDFLAG=NTACCESSRECWDFLAG
             IF NTSHARECOUNT<NTSHAREARRAYMAX THEN
              CALL NTADDTOSHARELIST
            END
          END
         UNTIL NTSHARECOUNT>=NTSHAREARRAYMAX
        END

       IF (NTPREFACCESSIDTYPE=2 OR NTPREFACCESSIDTYPE=1) AND
          (NTCROSSACCTSLTYPE=NTSLTYPELOAN OR
           NTCROSSACCTSLTYPE=NTSLTYPESHARELOAN) THEN
        DO
         FOR EACH LOAN WITH (LOAN:CHARGEOFFDATE=NTDATENULL AND
                             NTEXCLUDELOANTYPELIST(LOAN:TYPE)=FALSE)

          DO
           IF NTPREFACCESSIDTYPE=2 OR
             (NTPREFACCESSIDTYPE=1 AND NTPREFACCESSID=LOAN:ID) THEN
            DO
             NTCROSSACCTFLAG=TRUE
             NTACCESSIQFLAG=NTACCESSRECIQFLAG
             NTACCESSXFEROUTFLAG=NTACCESSRECXFEROUTFLAG
             NTACCESSXFERINFLAG=NTACCESSRECXFERINFLAG
             NTACCESSWDFLAG=NTACCESSRECWDFLAG
	     IF NTLOANCOUNT<NTLOANARRAYMAX THEN
              CALL NTADDTOLOANLIST
            END
	  END
         UNTIL NTLOANCOUNT>=NTLOANARRAYMAX
        END
      END
    END
  END
END

PROCEDURE NTSORTXACCOUNTSSHARE

END

PROCEDURE NTFINDSHARE

 NTFINDACCTNUM=""
 CALL NTFINDSHAREACCT
END

PROCEDURE NTFINDSHAREACCT

 NTFINDINDEX=0
 NTSLIDFOUND=FALSE
 NTI=1
 WHILE NTSLIDFOUND=FALSE AND NTI<=NTSHARECOUNT
  DO
   IF NTFINDID=NTSHAREID(NTI) AND
     (NTFINDACCTNUM="" OR NTFINDACCTNUM=NTSHAREACCTNUM(NTI))THEN
    DO
     NTSLIDFOUND=TRUE
     NTFINDINDEX=NTI
    END
   NTI=NTI+1
  END
END

PROCEDURE NTSETSHARESORTORDER

 NTSHARESORTORDER(NTSORTINDEX)=NTSORTORDER
 IF NTSORTORDER>NTSHAREMAXSORT THEN
  NTSHAREMAXSORT=NTSORTORDER
END

PROCEDURE NTBUILDLOANLIST

 NTLOANMAXSORT=0
 CALL NTSETSERVICELISTS
[
 CALL NTSETEXCLUDELOANLIST
 CALL NTSETCLOSEDLOANLIST
]

 NTLOANCOUNT=0
 NTTOTALLOANBALANCE=$0.00
 FOR EACH LOAN WITH (LOAN:CHARGEOFFDATE=NTDATENULL
[ AND
		     NTEXCLUDELOANTYPEALLFLAG=FALSE
] AND
		     NTEXCLUDELOANTYPELIST(LOAN:TYPE)=FALSE)
  DO
   NTCROSSACCTFLAG=FALSE
   NTACCESSIQFLAG=TRUE
   NTACCESSXFEROUTFLAG=TRUE
   NTACCESSXFERINFLAG=TRUE
   NTACCESSWDFLAG=TRUE
   CALL NTADDTOLOANLIST
  END
 UNTIL NTLOANCOUNT>=NTLOANARRAYMAX

 IF NTCROSSACCTACCESS=TRUE THEN
  DO
   NTCROSSACCTSLTYPE=NTSLTYPELOAN
   CALL NTADDCROSSACCOUNTS
  END

 CALL NTSORTXACCOUNTSLOAN
END

PROCEDURE NTADDTOLOANLIST

 CALL NTCHECKUSERHBENABLE

[Check service codes]
 NTSERVICEXFERINFLAG=FALSE
 NTSERVICEXFEROUTFLAG=FALSE
 NTSERVICEWDFLAG=FALSE
 IF  NTSERVICEXFERINALLFLAG=TRUE THEN
  NTSERVICEXFERINFLAG=TRUE
 ELSE IF NTSERVICEXFEROUTALLFLAG=TRUE THEN
  NTSERVICEXFEROUTFLAG=TRUE
 ELSE IF NTSERVICEWDALLFLAG=TRUE THEN
  NTSERVICEWDFLAG=TRUE

 NTI=1
 WHILE (NTSERVICEXFERINFLAG=FALSE OR
        NTSERVICEXFEROUTFLAG=FALSE OR
	NTSERVICEWDFLAG=FALSE) AND
        NTI<=99
  DO
   IF NTSERVICEXFERINLIST(NTI)=TRUE THEN
    DO
     IF ANYSERVICE(LOAN,NTI) THEN
      NTSERVICEXFERINFLAG=TRUE
    END
   IF NTSERVICEXFEROUTLIST(NTI)=TRUE THEN
    DO
     IF ANYSERVICE(LOAN,NTI) THEN
      NTSERVICEXFEROUTFLAG=TRUE
    END
   IF NTSERVICEWDLIST(NTI)=TRUE THEN
    DO
     IF ANYSERVICE(LOAN,NTI) THEN
      NTSERVICEWDFLAG=TRUE
    END
   NTI=NTI+1
  END

 IF NTSERVICEXFERINFLAG=FALSE THEN
  NTACCESSXFERINFLAG=FALSE
 IF NTSERVICEXFEROUTFLAG=FALSE THEN
  NTACCESSXFEROUTFLAG=FALSE
 IF NTSERVICEWDFLAG=FALSE THEN
  NTACCESSWDFLAG=FALSE

 NTACCESSFLAG=FALSE
 IF NTACCESSTYPE=NTACCESSTYPEIQ AND
    NTACCESSIQFLAG=TRUE THEN
  NTACCESSFLAG=TRUE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEXFERIN AND
         NTACCESSXFERINFLAG=TRUE THEN
  NTACCESSFLAG=TRUE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEXFEROUT AND
         NTACCESSXFEROUTFLAG=TRUE THEN
  NTACCESSFLAG=TRUE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEWD AND
         NTACCESSWDFLAG=TRUE THEN
  NTACCESSFLAG=TRUE

[*  Now remove access based on restictions
*]
 IF LOAN:AVAILABLECREDIT<=$0.00 AND
   (NTACCESSTYPE=NTACCESSTYPEXFEROUT OR
    NTACCESSTYPE=NTACCESSTYPEWD) THEN
  NTACCESSFLAG=FALSE

 IF NTACCESSTYPE=NTACCESSTYPEIQ AND
    NTCROSSACCTFLAG=TRUE AND
    JHAINFOCROSSACCTIQFLAG=FALSE THEN
  NTACCESSFLAG=FALSE

 IF NTACCESSTYPE<>NTACCESSTYPEIQ AND
    LOAN:CLOSEDATE<>NTDATENULL THEN
  NTACCESSFLAG=FALSE
 ELSE IF NTACCESSTYPE=NTACCESSTYPEIQ THEN
  DO
   NTCLOSEDAVAILFLAG=FALSE
   IF LOAN:CLOSEDATE=NTDATENULL OR
      LOAN:CLOSEDATE>SYSTEMDATE THEN
    NTCLOSEDAVAILFLAG=TRUE
   ELSE
    DO
     NTI=1
     NTDONE=FALSE
     WHILE NTI<=JHAINFOCLOSEDSLLISTMAX AND NTDONE=FALSE
      DO
       IF NTCLOSEDLOANTYPEALLFLAG(NTI)=TRUE OR
         (NTCLOSEDLOANTYPENONEFLAG(NTI)=FALSE AND
          NTCLOSEDLOANTYPELIST(NTI,LOAN:TYPE)=TRUE) THEN
        DO
         NTDONE=TRUE
         IF JHAINFOCLOSEDLOANAVAILDAYS(NTI)=9999 OR
           (JHAINFOCLOSEDLOANAVAILDAYS(NTI)<>0 AND
            SYSTEMDATE-LOAN:CLOSEDATE<=
	    JHAINFOCLOSEDLOANAVAILDAYS(NTI)) THEN
          NTCLOSEDAVAILFLAG=TRUE
        END
       NTI=NTI+1
      END
    END

   IF NTCLOSEDAVAILFLAG=FALSE THEN
    NTACCESSFLAG=FALSE
  END

[*  This block of code just checks for duplicates since ACCESS records
**  can point to duplicate shares/loans.
*]
 IF NTCROSSACCTFLAG=FALSE THEN
  NTSLIDFOUND=FALSE
 ELSE
  DO
   NTFINDID=LOAN:ID
   NTFINDACCTNUM=ACCOUNT:NUMBER
   CALL NTFINDLOANACCT
  END

 IF NTACCESSFLAG=TRUE AND NTSLIDFOUND=FALSE THEN
  DO
   NTLOANCOUNT=NTLOANCOUNT+1
   NTTOTALLOANBALANCE=NTTOTALLOANBALANCE+LOAN:BALANCE
   NTLOANIQFLAG(NTLOANCOUNT)=NTACCESSIQFLAG
   NTLOANXFERINFLAG(NTLOANCOUNT)=NTACCESSXFERINFLAG
   NTLOANXFEROUTFLAG(NTLOANCOUNT)=NTACCESSXFEROUTFLAG
   NTLOANWDFLAG(NTLOANCOUNT)=NTACCESSWDFLAG
   NTLOANACCTNUM(NTLOANCOUNT)=ACCOUNT:NUMBER
   NTLOANID(NTLOANCOUNT)=LOAN:ID
   NTLOANTYPE(NTLOANCOUNT)=LOAN:TYPE
   NTLOANDESC(NTLOANCOUNT)=LOAN:DESCRIPTION
   NTLOANBALANCE(NTLOANCOUNT)=LOAN:BALANCE
   NTLOANDUEDATE(NTLOANCOUNT)=LOAN:DUEDATE
   NTLOANPAYMENT(NTLOANCOUNT)=LOAN:PAYMENT
   NTLOANPAYMENTDUE(NTLOANCOUNT)=LOAN:PAYMENTDUE
   NTLOANPASTDUEAMOUNT(NTLOANCOUNT)=LOAN:PASTDUEAMOUNT
   CALL NTFINDLOANPMTBUCKETAMT
   NTLOANPMTBUCKETAMT(NTLOANCOUNT)=NTPMTBUCKETAMT
   NTLOANLOANCODE(NTLOANCOUNT)=LOAN:LOANCODE
   NTLOANCREDITLIMIT(NTLOANCOUNT)=LOAN:CREDITLIMIT
   NTLOANEFFECTIVEBAL(NTLOANCOUNT)=LOAN:BALANCE
   IF LOAN:INTERESTTYPE>9 AND LOAN:CLOSEDATE=NTDATENULL THEN
    NTLOANEFFECTIVEBAL(NTLOANCOUNT)=NTLOANEFFECTIVEBAL(NTLOANCOUNT)+
     LOAN:INTERESTDUE+LOAN:LATECHARGEDUE
   NTLOANAVAILCREDIT(NTLOANCOUNT)=LOAN:AVAILABLECREDIT
   NTLOANSORTORDER(NTLOANCOUNT)=0
   CALL NTGETLOANDISPLAYNAME
   NTLOANDISPLAYNAME(NTLOANCOUNT)=NTDISPLAYNAME
   NTLOANDISPLAYNAMEBAL(NTLOANCOUNT)=NTDISPLAYNAMEBAL

   NTLOANACCOUNTNAME(NTLOANCOUNT)=NAME:LONGNAME
   NTLOANNICKNAME(NTLOANCOUNT)=LOAN:NICKNAME

  END
END

PROCEDURE NTGETLOANDISPLAYNAME

 IF LOAN:NICKNAME<>"" AND NTCROSSACCTFLAG=FALSE THEN
  NTDISPLAYNAME=LOAN:NICKNAME
 ELSE
  DO
   IF NTCROSSACCTFLAG=TRUE AND NTNAMEINCROSSACCTDISPLAY=TRUE THEN
    NTDISPLAYNAME=NAME:SHORTNAME+" "
   ELSE
    NTDISPLAYNAME=""
   NTACCTNUM=ACCOUNT:NUMBER
   CALL NTGETACCTNUMMASKED
   NTDISPLAYNAME=NTDISPLAYNAME+NTACCTNUMMASKED+" L"+LOAN:ID
  END
 NTDISPLAYNAMEBAL=NTDISPLAYNAME+
  " (Avail: "+FORMAT("###,###,##9.99",LOAN:AVAILABLECREDIT)+")"
END

PROCEDURE NTFINDLOAN

 NTFINDACCTNUM=""
 CALL NTFINDLOANACCT
END

PROCEDURE NTFINDLOANACCT

 NTFINDINDEX=0
 NTSLIDFOUND=FALSE
 NTI=1
 WHILE NTSLIDFOUND=FALSE AND NTI<=NTLOANCOUNT
  DO
   IF NTFINDID=NTLOANID(NTI)  AND
     (NTFINDACCTNUM="" OR NTFINDACCTNUM=NTLOANACCTNUM(NTI))THEN
    DO
     NTSLIDFOUND=TRUE
     NTFINDINDEX=NTI
    END
   NTI=NTI+1
  END
END

PROCEDURE NTSETLOANSORTORDER

 NTLOANSORTORDER(NTSORTINDEX)=NTSORTORDER
 IF NTSORTORDER>NTLOANMAXSORT THEN
  NTLOANMAXSORT=NTSORTORDER
END

PROCEDURE NTSORTXACCOUNTSLOAN

END

PROCEDURE NTBUILDEXTLOANLIST

 NTLOANMAXSORT=0
 NTEXTLOANCOUNT=0
 NTTOTALEXTLOANBALANCE=$0.00
 CALL NTEXTLOANLISTEXTLOANADD
 CALL NTEXTLOANLISTTRACKINGADD
END

PROCEDURE NTEXTLOANLISTEXTLOANADD

 FOR EACH EXTERNALLOAN
  DO
   NTACCESSFLAG=TRUE

   NTEXTLOANTYPEINDEX=0
   NTFOUND=FALSE
   NTI=1
   WHILE NTFOUND=FALSE AND NTI<=JHAINFOEXTLOANTYPESMAX
    DO
     IF JHAINFOEXTLOANTYPE(NTI)<>0 AND
        EXTERNALLOAN:TYPE=JHAINFOEXTLOANTYPE(NTI) THEN
      DO
       NTFOUND=TRUE
       NTEXTLOANTYPEINDEX=NTI
      END
     NTI=NTI+1
    END
   IF NTFOUND=FALSE THEN
    NTACCESSFLAG=FALSE

   IF EXTERNALLOAN:STATUS=1 OR
      EXTERNALLOAN:CLOSEDATE<>NTDATENULL THEN
    NTACCESSFLAG=FALSE

   IF NTACCESSFLAG=TRUE AND
    ((NTACCESSTYPE=NTACCESSTYPEIQ AND
      JHAINFOEXTLOANIQACCESSFLAG(NTEXTLOANTYPEINDEX)=TRUE) OR
     (NTACCESSTYPE=NTACCESSTYPEXFEROUT AND
      JHAINFOEXTLOANPMTACCESS(NTEXTLOANTYPEINDEX)<>0)) THEN
    DO
     NTEXTLOANCOUNT=NTEXTLOANCOUNT+1
     NTEXTLOANMASK=NTEXTLOANTYPESMASK(NTEXTLOANTYPEINDEX)
     NTEXTLOANDESC(NTEXTLOANCOUNT)=
      JHAINFOEXTLOANDESCRIPTION(NTEXTLOANTYPEINDEX)
     NTEXTLOANIQFLAG(NTEXTLOANCOUNT)=
      JHAINFOEXTLOANIQACCESSFLAG(NTEXTLOANTYPEINDEX)
     IF JHAINFOEXTLOANPMTACCESS(NTEXTLOANTYPEINDEX)<>0 THEN
      NTEXTLOANXFEROUTFLAG(NTEXTLOANCOUNT)=TRUE
     ELSE
      NTEXTLOANXFEROUTFLAG(NTEXTLOANCOUNT)=FALSE
     NTEXTLOANXFERINFLAG(NTEXTLOANCOUNT)=FALSE
     NTEXTLOANWDFLAG(NTEXTLOANCOUNT)=FALSE
     NTEXTLOANSORTORDER(NTEXTLOANCOUNT)=0

     NTEXTLOANTRACKINGTYPE(NTEXTLOANCOUNT)=-1
     NTEXTLOANTYPE(NTEXTLOANCOUNT)=EXTERNALLOAN:TYPE
     NTEXTLOANLOC(NTEXTLOANCOUNT)=EXTERNALLOAN:LOCATOR
     NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=EXTERNALLOAN:ACCOUNTNUMBER
     NTEXTLOANBALANCE(NTEXTLOANCOUNT)=EXTERNALLOAN:BALANCE
     NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=EXTERNALLOAN:PAYMENTDUE
     NTEXTLOANPIPAYMENT(NTEXTLOANCOUNT)=EXTERNALLOAN:PRINCINTPAYMENT
     NTEXTLOANESCPAYMENT(NTEXTLOANCOUNT)=EXTERNALLOAN:ESCROWPAYMENT
     NTEXTLOANIMPOUNDPAYMENT(NTEXTLOANCOUNT)=EXTERNALLOAN:IMPOUNDPAYMENT
     NTEXTLOANLATEFEE(NTEXTLOANCOUNT)=EXTERNALLOAN:LATECHARGEDUE
     NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=EXTERNALLOAN:DUEDATE
     NTEXTLOANAVAILCREDIT(NTEXTLOANCOUNT)=EXTERNALLOAN:AVAILCREDIT
     NTEXTLOANCREDITLIMIT(NTEXTLOANCOUNT)=EXTERNALLOAN:CREDITLIMIT
     NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=EXTERNALLOAN:PRINCINTPAYMENT+
      EXTERNALLOAN:ESCROWPAYMENT

     NTEXTLOANNUM=NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)
     CALL NTGETEXTLOANNUMMASKED
     NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)=NTEXTLOANNUMMASKED
     NTEXTLOANDISPLAYNAMEBAL(NTEXTLOANCOUNT)=
      NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)

     NTTOTALEXTLOANBALANCE=NTTOTALEXTLOANBALANCE+
      NTEXTLOANBALANCE(NTEXTLOANCOUNT)
    END
  END
 UNTIL NTEXTLOANCOUNT>=NTEXTLOANARRAYMAX
END

PROCEDURE NTEXTLOANLISTTRACKINGADD

 FOR EACH TRACKING WITH((TRACKING:EXPIREDATE='--/--/--' OR
                         TRACKING:EXPIREDATE>SYSACTUALDATE) AND
		        (TRACKING:TYPE=NTTRACKINGTYPEMC OR
			 TRACKING:TYPE=NTTRACKINGTYPEFICS OR
			 TRACKING:TYPE=NTTRACKINGTYPECC OR
		        (TRACKING:TYPE>=NTTRACKINGTYPEUSERDEFMIN AND
		         TRACKING:TYPE<=NTTRACKINGTYPEUSERDEFMAX)))
  DO
   NTEXTLOANTYPEINDEX=0
   NTFOUND=FALSE
   NTI=1
   WHILE NTFOUND=FALSE AND NTI<=JHAINFOEXTLOANTYPESMAX
    DO
     IF JHAINFOEXTLOANTYPE(NTI)=0 AND
        TRACKING:TYPE=JHAINFOEXTLOANTRACKINGTYPE(NTI) THEN
      DO
       NTFOUND=TRUE
       NTEXTLOANTYPEINDEX=NTI
      END
     NTI=NTI+1
    END

   IF NTFOUND=TRUE AND
    ((NTACCESSTYPE=NTACCESSTYPEIQ AND
      JHAINFOEXTLOANIQACCESSFLAG(NTEXTLOANTYPEINDEX)=TRUE) OR
     (NTACCESSTYPE=NTACCESSTYPEXFEROUT AND
      JHAINFOEXTLOANPMTACCESSFLAG(NTEXTLOANTYPEINDEX)=TRUE)) THEN
    DO
     NTEXTLOANCOUNT=NTEXTLOANCOUNT+1
     NTEXTLOANMASK=NTEXTLOANTYPESMASK(NTEXTLOANTYPEINDEX)
     NTEXTLOANDESC(NTEXTLOANCOUNT)=
      JHAINFOEXTLOANDESCRIPTION(NTEXTLOANTYPEINDEX)
     NTEXTLOANIQFLAG(NTEXTLOANCOUNT)=
      JHAINFOEXTLOANIQACCESSFLAG(NTEXTLOANTYPEINDEX)
     NTEXTLOANXFEROUTFLAG(NTEXTLOANCOUNT)=
      JHAINFOEXTLOANPMTACCESSFLAG(NTEXTLOANTYPEINDEX)
     NTEXTLOANXFERINFLAG(NTEXTLOANCOUNT)=FALSE
     NTEXTLOANWDFLAG(NTEXTLOANCOUNT)=FALSE

     NTEXTLOANTYPE(NTEXTLOANCOUNT)=0
     NTEXTLOANTRACKINGTYPE(NTEXTLOANCOUNT)=TRACKING:TYPE
     NTEXTLOANTRACKINGLOC(NTEXTLOANCOUNT)=TRACKING:LOCATOR

[*  Take away the transfer out access for user-defined external loans
**  that don't use TRACKING:USERCHAR1 for the account number.  Episys
**  will not allow payments to these external loans if USERCHAR1 does
**  not contain the account number.
*]
     IF TRACKING:TYPE>=NTTRACKINGTYPEUSERDEFMIN AND
	TRACKING:TYPE<=NTTRACKINGTYPEUSERDEFMAX AND
	JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)<>1 THEN
      NTEXTLOANXFEROUTFLAG(NTEXTLOANCOUNT)=FALSE

     NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=""
     NTEXTLOANBALANCE(NTEXTLOANCOUNT)=$0.00
     NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=$0.00
     NTEXTLOANESCPAYMENT(NTEXTLOANCOUNT)=$0.00
     NTEXTLOANLATEFEE(NTEXTLOANCOUNT)=$0.00
     NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=$0.00
     NTEXTLOANAVAILCREDIT(NTEXTLOANCOUNT)=$0.00
     NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=NTDATENULL
     NTEXTLOANSORTORDER(NTEXTLOANCOUNT)=0

     IF TRACKING:TYPE=NTTRACKINGTYPEMC THEN
      DO
       NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR1
       NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT1
       NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT3
       NTEXTLOANESCPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT4
       NTEXTLOANLATEFEE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT5
       NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT6
       NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE2

       NTEXTLOANNUM=NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)
       CALL NTGETEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)=NTEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAMEBAL(NTEXTLOANCOUNT)=
        NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)
      END
     ELSE IF TRACKING:TYPE=NTTRACKINGTYPEFICS THEN
      DO
       NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR1
       NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT1
       NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT3
       NTEXTLOANESCPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT4
       NTEXTLOANLATEFEE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT5
       NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT6
       NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE2

       NTEXTLOANNUM=NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)
       CALL NTGETEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)=NTEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAMEBAL(NTEXTLOANCOUNT)=
        NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)
      END
     ELSE IF TRACKING:TYPE=NTTRACKINGTYPECC THEN
      DO
       NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR1
       NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT1
       NTEXTLOANAVAILCREDIT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT2
       NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT5
       NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT5
       NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE2

       NTEXTLOANNUM=NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)
       CALL NTGETEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)=NTEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAMEBAL(NTEXTLOANCOUNT)=
        NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)
      END
     ELSE
      DO
       IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=1 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR1
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=2 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR2
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=3 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR3
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=4 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR4
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=5 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR5
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=6 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR6
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=7 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR7
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=8 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR8
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=9 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR9
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=10 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR10
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=11 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR11
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=12 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR12
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=13 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR13
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=14 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR14
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=15 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR15
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=16 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR16
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=17 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR17
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=18 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR18
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=19 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR19
       ELSE IF JHAINFOEXTLOANACCTNUMCHRFLD(NTEXTLOANTYPEINDEX)=20 THEN
        NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)=TRACKING:USERCHAR20

       IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=1 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT1
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=2 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT2
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=3 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT3
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=4 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT4
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=5 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT5
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=6 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT6
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=7 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT7
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=8 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT8
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=9 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT9
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=10 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT10
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=11 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT11
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=12 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT12
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=13 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT13
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=14 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT14
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=15 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT15
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=16 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT16
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=17 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT17
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=18 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT18
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=19 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT19
       ELSE IF JHAINFOEXTLOANBALANCEAMTFLD(NTEXTLOANTYPEINDEX)=20 THEN
        NTEXTLOANBALANCE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT20

       IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=1 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT1
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=2 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT2
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=3 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT3
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=4 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT4
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=5 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT5
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=6 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT6
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=7 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT7
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=8 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT8
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=9 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT9
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=10 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT10
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=11 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT11
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=12 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT12
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=13 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT13
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=14 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT14
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=15 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT15
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=16 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT16
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=17 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT17
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=18 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT18
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=19 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT19
       ELSE IF JHAINFOEXTLOANPAYMENTAMTFLD(NTEXTLOANTYPEINDEX)=20 THEN
        NTEXTLOANPAYMENT(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT20

       IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=1 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT1
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=2 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT2
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=3 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT3
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=4 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT4
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=5 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT5
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=6 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT6
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=7 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT7
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=8 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT8
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=9 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT9
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=10 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT10
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=11 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT11
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=12 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT12
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=13 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT13
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=14 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT14
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=15 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT15
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=16 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT16
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=17 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT17
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=18 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT18
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=19 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT19
       ELSE IF JHAINFOEXTLOANAMOUNTDUEAMTFLD(NTEXTLOANTYPEINDEX)=20 THEN
        NTEXTLOANAMOUNTDUE(NTEXTLOANCOUNT)=TRACKING:USERAMOUNT20

       IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=1 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE1
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=2 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE2
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=3 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE3
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=4 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE4
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=5 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE5
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=6 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE6
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=7 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE7
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=8 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE8
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=9 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE9
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=10 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE10
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=11 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE11
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=12 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE12
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=13 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE13
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=14 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE14
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=15 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE15
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=16 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE16
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=17 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE17
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=18 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE18
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=19 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE19
       ELSE IF JHAINFOEXTLOANDUEDATEDATEFLD(NTEXTLOANTYPEINDEX)=20 THEN
        NTEXTLOANDUEDATE(NTEXTLOANCOUNT)=TRACKING:USERDATE20

       NTEXTLOANNUM=NTEXTLOANACCOUNTNUM(NTEXTLOANCOUNT)
       CALL NTGETEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)=NTEXTLOANNUMMASKED
       NTEXTLOANDISPLAYNAMEBAL(NTEXTLOANCOUNT)=
        NTEXTLOANDISPLAYNAME(NTEXTLOANCOUNT)
      END

     NTTOTALEXTLOANBALANCE=NTTOTALEXTLOANBALANCE+
      NTEXTLOANBALANCE(NTEXTLOANCOUNT)
    END
  END
 UNTIL NTEXTLOANCOUNT>=NTEXTLOANARRAYMAX
END

PROCEDURE NTSETEXTLOANSORTORDER

 NTEXTLOANSORTORDER(NTSORTINDEX)=NTSORTORDER
 IF NTSORTORDER>NTEXTLOANMAXSORT THEN
  NTEXTLOANMAXSORT=NTSORTORDER
END

PROCEDURE NTFINDLOANPMTBUCKETAMT
[* NTLOANPAYMENT value from the pre-built loan information array may not
** be accurate when loan is delinquent. Some clients may choose not to
** display NTLOANPAYMENTDUE as includes payment + any delinquent amount.
** Provide another option to display next Payment based on PMTBUCKETS.
** Clients now have an option to choose from NTLOANPAYMENT,
** NTLOANPAYMENTDUE or NTLOANPMTBUCKETAMT to display in Payment column.
** Default remains NTLOANPAYMENT.
]

 NTPMTBUCKETAMT=$0.00
 IF LOAN:DUEDATE>=LOAN:PMTBUCKETDUEDATE:1 OR
    LOAN:PMTBUCKETDUEDATE:1='--/--/--' THEN
  NTPMTBUCKETAMT=LOAN:PAYMENT
 ELSE
  DO
   NTI=1
   NTFOUND=FALSE
   WHILE NTFOUND=FALSE AND NTI<=7
    DO
     IF LOAN:DUEDATE>=LOAN:PMTBUCKETDUEDATE:(NTI) AND
        LOAN:PMTBUCKETDUEDATE:(NTI)<>'--/--/--' THEN
      DO
       NTPMTBUCKETAMT=LOAN:PMTBUCKETAMOUNT:(NTI)
       NTFOUND=TRUE
      END
     NTI=NTI+1
    END
  END
END

PROCEDURE NTSETSERVICELISTS

 IF NTSERVICELISTSETFLAG=FALSE THEN
  DO
   LELISTINPUT=GETDATACHARACTER(GETPARAMSYMXCHCLSERVICESXIN,SYMXINSTANCE,SYMXCLIENTNUM)

   BNODEBUGMSG="GETPARAMSYMXCHCLSERVICESXIN:"+LELISTINPUT
   CALL BNODEBUGMSGADD

   CALL LISTEXPAND
   NTSERVICEXFERINALLFLAG=LEALLFLAG
   FOR NTI=0 TO 99
    DO
     NTSERVICEXFERINLIST(NTI)=LELIST(NTI)
    END

   LELISTINPUT=GETDATACHARACTER(GETPARAMSYMXCHCLSERVICESXOUT,SYMXINSTANCE,SYMXCLIENTNUM)

   BNODEBUGMSG="GETPARAMSYMXCHCLSERVICESXOUT:"+LELISTINPUT
   CALL BNODEBUGMSGADD

   CALL LISTEXPAND
   NTSERVICEXFEROUTALLFLAG=LEALLFLAG
   FOR NTI=0 TO 99
    DO
     NTSERVICEXFEROUTLIST(NTI)=LELIST(NTI)
    END
   LELISTINPUT=GETDATACHARACTER(GETPARAMSYMXCHCLSERVICESWD,SYMXINSTANCE,SYMXCLIENTNUM)

   BNODEBUGMSG="GETPARAMSYMXCHCLSERVICESWD:"+LELISTINPUT
   CALL BNODEBUGMSGADD

   CALL LISTEXPAND
   NTSERVICEWDALLFLAG=LEALLFLAG
   FOR NTI=0 TO 99
    DO
     NTSERVICEWDLIST(NTI)=LELIST(NTI)
    END

   NTSERVICELISTSETFLAG=TRUE
  END
END

PROCEDURE NTSETEXCLUDESHARELIST

 IF NTEXCLUDESHARELISTSETFLAG=FALSE THEN
  DO
   LELISTINPUT=JHAINFOEXCLUDESHARETYPELIST
   CALL LISTEXPAND
   NTEXCLUDESHARETYPEALLFLAG=LEALLFLAG
   FOR NTI=0 TO 9999
    DO
     NTEXCLUDESHARETYPELIST(NTI)=LELIST(NTI)
    END

   NTEXCLUDESHARELISTSETFLAG=TRUE
  END
END

PROCEDURE NTSETEXCLUDELOANLIST

 IF NTEXCLUDELOANLISTSETFLAG=FALSE THEN
  DO
   LELISTINPUT=JHAINFOEXCLUDELOANTYPELIST
   CALL LISTEXPAND
   NTEXCLUDELOANTYPEALLFLAG=LEALLFLAG
   FOR NTI=0 TO 9999
    DO
     NTEXCLUDELOANTYPELIST(NTI)=LELIST(NTI)
    END

   NTEXCLUDELOANLISTSETFLAG=TRUE
  END
END

PROCEDURE NTSETSTOPPMTSHARELIST

 IF NTSTOPPMTSHARELISTSETFLAG=FALSE THEN
  DO
   LELISTINPUT=JHAINFOSTOPPMTSHARETYPELIST
   CALL LISTEXPAND
   NTSTOPPMTSHARETYPEALLFLAG=LEALLFLAG
   FOR NTI=0 TO 9999
    DO
     NTSTOPPMTSHARETYPELIST(NTI)=LELIST(NTI)
    END

   NTSTOPPMTSHARELISTSETFLAG=TRUE
  END
END

PROCEDURE NTSETSTOPPMTLOANLIST

 IF NTSTOPPMTLOANLISTSETFLAG=FALSE THEN
  DO
   LELISTINPUT=JHAINFOSTOPPMTLOANTYPELIST
   CALL LISTEXPAND
   NTSTOPPMTLOANTYPEALLFLAG=LEALLFLAG
   FOR NTI=0 TO 9999
    DO
     NTSTOPPMTLOANTYPELIST(NTI)=LELIST(NTI)
    END

   NTSTOPPMTLOANLISTSETFLAG=TRUE
  END
END

PROCEDURE NTSETCLOSEDSHARELIST

 IF NTCLOSEDSHARELISTSETFLAG=FALSE THEN
  DO
   FOR NTI=0 TO JHAINFOCLOSEDSLLISTMAX
    DO
     LELISTINPUT=JHAINFOCLOSEDSHARETYPELIST(NTI)
     CALL LISTEXPAND
     NTCLOSEDSHARETYPEALLFLAG(NTI)=LEALLFLAG
     IF LOWERCASE(LELISTINPUT)="none" THEN
      NTCLOSEDSHARETYPENONEFLAG(NTI)=TRUE
     ELSE
      DO
       NTCLOSEDSHARETYPENONEFLAG(NTI)=FALSE
       FOR NTJ=0 TO 9999
        DO
         NTCLOSEDSHARETYPELIST(NTI,NTJ)=LELIST(NTJ)
        END
      END
    END

   NTCLOSEDSHARELISTSETFLAG=TRUE
  END
END

PROCEDURE NTSETCLOSEDLOANLIST

 IF NTCLOSEDLOANLISTSETFLAG=FALSE THEN
  DO
   FOR NTI=0 TO JHAINFOCLOSEDSLLISTMAX
    DO
     LELISTINPUT=JHAINFOCLOSEDLOANTYPELIST(NTI)
     CALL LISTEXPAND
     NTCLOSEDLOANTYPEALLFLAG(NTI)=LEALLFLAG
     IF LOWERCASE(LELISTINPUT)="none" THEN
      NTCLOSEDLOANTYPENONEFLAG(NTI)=TRUE
     ELSE
      DO
       NTCLOSEDLOANTYPENONEFLAG(NTI)=FALSE
       FOR NTJ=0 TO 9999
        DO
         NTCLOSEDLOANTYPELIST(NTI,NTJ)=LELIST(NTJ)
        END
      END
    END

   NTCLOSEDLOANLISTSETFLAG=TRUE
  END
END

PROCEDURE NTSETUSERHBENABLE

 NTUSERHBENABLE=NTHBENABLEIQ
 FOR EACH PREFERENCE WITH (PREFERENCE:LOCATOR=@PREFLOCATOR)
  DO
   NTUSERHBENABLE=PREFERENCE:HBENABLE
  END
END

PROCEDURE NTCHECKUSERHBENABLE

 IF NTUSERHBENABLE=NTHBENABLEIQ THEN
  DO
   NTACCESSXFEROUTFLAG=FALSE
   NTACCESSXFERINFLAG=FALSE
   NTACCESSWDFLAG=FALSE
  END
 ELSE IF NTUSERHBENABLE=NTHBENABLEXF THEN
  NTACCESSWDFLAG=FALSE
END


PROCEDURE NTGETACCTNUMMASKED

 IF NTACCOUNTMASKFLAG=FALSE THEN
  NTACCTNUMMASKED=NTACCTNUM
 ELSE
  DO
   NTACCTNUMMASKED=""
   FOR NTI=1 TO LENGTH(NTACCOUNTMASK)
    DO
     IF SEGMENT(NTACCOUNTMASK,NTI,NTI)<>"1" THEN
      NTACCTNUMMASKED=NTACCTNUMMASKED+SEGMENT(NTACCOUNTMASK,NTI,NTI)
     ELSE
      NTACCTNUMMASKED=NTACCTNUMMASKED+SEGMENT(NTACCTNUM,NTI,NTI)
    END
  END
END

PROCEDURE NTGETEXTLOANNUMMASKED

 IF NTEXTLOANMASKFLAG=FALSE THEN
  NTEXTLOANNUMMASKED=NTEXTLOANNUM
 ELSE
  DO
   NTEXTLOANNUMMASKED=""
   FOR NTI=LENGTH(NTEXTLOANNUM) TO 1 BY -1
    DO
     NTREVERSEPOS=LENGTH(NTEXTLOANNUM)-NTI+1
     IF NTREVERSEPOS>LENGTH(NTEXTLOANMASK) THEN
      NTEXTLOANNUMMASKED=
       SEGMENT(NTEXTLOANNUM,NTI,NTI)+NTEXTLOANNUMMASKED
     ELSE IF SEGMENT(NTEXTLOANMASK,
                     LENGTH(NTEXTLOANMASK)-NTREVERSEPOS+1,
	             LENGTH(NTEXTLOANMASK)-NTREVERSEPOS+1)<>"1" THEN
      NTEXTLOANNUMMASKED=
       SEGMENT(NTEXTLOANMASK,LENGTH(NTEXTLOANMASK)-NTREVERSEPOS+1,
        LENGTH(NTEXTLOANMASK)-NTREVERSEPOS+1)+NTEXTLOANNUMMASKED
     ELSE
      NTEXTLOANNUMMASKED=
       SEGMENT(NTEXTLOANNUM,NTI,NTI)+NTEXTLOANNUMMASKED
    END
  END
END

