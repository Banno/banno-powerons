SYMCONNECT
STATELESS

TARGET=ACCOUNT

DEFINE
 Q                   = CHARACTER
 I                   = NUMBER
 J                   = NUMBER
 TOTALLOANS          = NUMBER
 ELIGIBLELOANS       = NUMBER
 WARNINGLOOP         = NUMBER
 SERVICEFOUND        = NUMBER
 SERVICELOOP         = NUMBER
 LOANSEASON          = NUMBER
 GRACEDAYS           = NUMBER
 SKIPCOUNT           = NUMBER
 TEMPLOOP            = NUMBER
 TIMEPERIOD          = CHARACTER(12)

 LID                 = CHARACTER(4)
 LDUEDATE            = DATE
 LNEWDUEDATE         = DATE
 LPAYMENT            = MONEY
 LMATURITYDATE       = DATE
 LNEWMATURITYDATE    = DATE
 LDESCRIPTION        = CHARACTER(40)
 LOANLOOP            = NUMBER
 LOANDISPLAYED       = NUMBER
 SHAREDISPLAYED      = NUMBER
 INELIGIBLEDISPLAYED = NUMBER


 LOANID              = CHARACTER(4)  ARRAY(25)
 LOANINELIGIBLECODE  = NUMBER        ARRAY(25,10)
 LOANSMAX            = 25
 INELIGIBLEREASONS   = CHARACTER(60) ARRAY(10)
 INELIGIBLEREASONSMAX= 10

 LOANTYPES           = NUMBER        ARRAY(9999)
 SHARETYPES          = NUMBER        ARRAY(9999)
 TYPESMAX            = 9999

 LOANWARNINGS        = NUMBER        ARRAY(999)
 ACCTWARNINGS        = NUMBER        ARRAY(999)
 WARNINGSMAX         = 999

 LOANSERVICECODE     = NUMBER        ARRAY(99)
 SERVICECODEMAX      = 99

 FEEAMOUNTS          = MONEY         ARRAY(99) [0:DEFAULT, 
                                                01-99:REL CODE]
 FEEAMOUNTSMAX       = 99

 TERMS	  	     = CHARACTER(132) ARRAY(40)
 TERMSMAXLINES	     = 40

 GETPRELOADDATA	     = "GETPRELOADDATA"
 PERFORMSKIPAPAYMENT = "PERFORMSKIPAPAYMENT"

 TRUE                      = 1
 FALSE                     = 0
 DATENULL                  = '--/--/--'

 INELIGIBLECODESET         = 0
 INELIGIBLECODELOANTYPE    = 1
 INELIGIBLECODELOANWARNING = 2
 INELIGIBLECODEMAXSKIPS    = 3
 INELIGIBLECODETIMESKIP    = 4
 INELIGIBLECODEACCTWARNING = 5
 INELIGIBLECODESERVICECODE = 6
 INELIGIBLECODEMINPAYMENT  = 7
 INELIGIBLECODEMAXPAYMENT  = 8
 INELIGIBLECODETIMEOPEN    = 9
 INELIGIBLECODEPASTDUE     = 10

 [These are customizable]
 FEEAMOUNT          = $35.00
 LOANTRACKINGTYPE   = 77
 MAXSKIPS           = 2
 MINMONTHSINCE      = 2
 SUBSOURCECODE      = 33 [Skip Payment Fee]
 OTHERACTION        = 1  [Use "FEE OTHER" OTHERACTION with Fee]
 FEECOMMENT         = ""
 LOANMINPAYMENT     = $0.00
 LOANMAXPAYMENT     = $0.00

 [Both start and end date must be set for this feature to work]
 STARTDATE          = '08/29/2019' 
 ENDDATE            = '12/29/2019'
END

SETUP
 Q=CTRLCHR(34)

 CALL INITIALIZETERMS
 TERMS(1)="Some example terms go here"
 TERMS(2)="Some more text"
 TERMS(3)="Even moar text!"

END [SETUP]

PRINT TITLE="Banno Skip a Loan Payment"
 CALL INITIALIZEDATA
 IF @RGSTATE=GETPRELOADDATA THEN
  DO
   CALL PROCGETPRELOADDATA
  END
 ELSE IF @RGSTATE=PERFORMSKIPAPAYMENT THEN
  DO
   CALL PROCPERFORMSKIPAPAYMENT
  END
END [PRINT]

PROCEDURE PROCGETPRELOADDATA
 PRINT "{"
 NEWLINE

 [ Check if skip-a-pay is available based on start and end dates.
   Both dates must be set to a non null value for this to work.
 ]
 PRINT "  "+Q+"available"+Q+":"
 IF STARTDATE<>DATENULL AND ENDDATE<>DATENULL THEN
  DO
   IF SYSTEMDATE>=STARTDATE AND SYSTEMDATE<=ENDDATE THEN
    PRINT "true,"
   ELSE
    PRINT "false,"
  END
 ELSE 
  PRINT "true,"
 NEWLINE

 PRINT "  "+Q+"availableStartDate"+Q+":"
 IF STARTDATE<>DATENULL then
  PRINT Q+FORMAT("99/99/9999",STARTDATE)+Q+","
 ELSE
  PRINT Q+Q+","
 NEWLINE
  
 PRINT "  "+Q+"availableEndDate"+Q+":"
 IF ENDDATE<>DATENULL then
  PRINT Q+FORMAT("99/99/9999",ENDDATE)+Q+","
 ELSE
  PRINT Q+Q+","
 NEWLINE 
 
 PRINT "  "+Q+"feePerPaymentSkip"+Q+":"+Q+FORMAT("##,###,##9.99",FEEAMOUNT)+Q+","
 NEWLINE

 PRINT "  "+Q+"terms"+Q+":["
 NEWLINE
 I=1
 WHILE I<TERMSMAXLINES AND TERMS(I)<>""
  DO
   IF I>1 THEN
    PRINT ","
   PRINT Q+TERMS(I)+" "+Q
   NEWLINE
   I=I+1
  END
 PRINT "],"
 NEWLINE

 CALL GETLOANDATA
 PRINT "  "+Q+"availableLoans"+Q+": ["
 NEWLINE

 LOANDISPLAYED=FALSE
 FOR LOANLOOP=1 TO TOTALLOANS
  DO
   IF LOANINELIGIBLECODE(LOANLOOP,INELIGIBLECODESET)=FALSE THEN
    DO
     LID=LOANID(LOANLOOP)
     CALL GETSKIPCRITERIA

     IF LOANDISPLAYED=TRUE THEN
      PRINT ","
     PRINT "{"
     NEWLINE
     PRINT Q+"name"+Q+": "+Q+LDESCRIPTION+Q+","
     NEWLINE
     PRINT Q+"paymentAmount"+Q+": "+Q+FORMAT("##,###,##9.99",LPAYMENT)+Q+","
     NEWLINE
     PRINT Q+"accountId"+Q+":"+Q+LID+Q+","
     NEWLINE
     PRINT Q+"originalPaymentDueDate"+Q+": "+Q+FORMAT("99/99/9999",LDUEDATE)+Q+","
     NEWLINE
     PRINT Q+"newPaymentDueDate"+Q+": "+Q+FORMAT("99/99/9999",LNEWDUEDATE)+Q
     NEWLINE
     PRINT "}"
     NEWLINE
     
     LOANDISPLAYED=TRUE
    END
  END
 PRINT "  ],"
 NEWLINE

 PRINT "  "+Q+"ineligibleLoans"+Q+": ["
 NEWLINE

 LOANDISPLAYED=FALSE
 FOR LOANLOOP=1 TO TOTALLOANS
  DO
   IF LOANINELIGIBLECODE(LOANLOOP,INELIGIBLECODESET)=TRUE THEN
    DO
     LID=LOANID(LOANLOOP)
     CALL GETSKIPCRITERIA

     IF LOANDISPLAYED=TRUE THEN
      PRINT ","
     PRINT "{"
     NEWLINE
     PRINT Q+"name"+Q+": "+Q+LDESCRIPTION+Q+","
     NEWLINE
     PRINT Q+"accountId"+Q+":"+Q+LID+Q+","
     NEWLINE
     INELIGIBLEDISPLAYED=FALSE
     FOR TEMPLOOP=1 TO INELIGIBLEREASONSMAX
      DO
       IF LOANINELIGIBLECODE(LOANLOOP,TEMPLOOP)=TRUE AND
          INELIGIBLEDISPLAYED=FALSE THEN
        DO
         PRINT Q+"ineligibilityReason"+Q+": "+Q+INELIGIBLEREASONS(TEMPLOOP)+Q
         NEWLINE
	 INELIGIBLEDISPLAYED=TRUE
        END
      END

     PRINT "}"
     NEWLINE
     
     LOANDISPLAYED=TRUE
    END
  END
 PRINT "  ],"
 NEWLINE

[    "availableShares":
      {
        "name":"REGULAR SHARES",
        "accountId":"123451234512345",
        "balance":"233.92"
      }
]    
 PRINT "  "+Q+"availableShares"+Q+": ["
 NEWLINE

 SHAREDISPLAYED=FALSE
 FOR EACH SHARE WITH (SHARE:CLOSEDATE=DATENULL AND
                      SHARE:CHARGEOFFDATE=DATENULL AND 
                      SHARE:AVAILABLEBALANCE>=FEEAMOUNT AND 
                      SHARETYPES(SHARE:TYPE)=TRUE)
  DO
   IF SHAREDISPLAYED=TRUE THEN
    PRINT ","
   PRINT "{"+Q+"name"+Q+": "+Q+SHARE:DESCRIPTION+Q+","
   NEWLINE
   PRINT Q+"accountId"+Q+": "+Q+SHARE:ID+Q+","
   NEWLINE
   PRINT Q+"balance"+Q+": "+Q+FORMAT("##,###,##9.99",SHARE:AVAILABLEBALANCE)+Q+"}"
   NEWLINE
   SHAREDISPLAYED=TRUE
  END 
 PRINT "]}"
 NEWLINE

END

PROCEDURE PROCPERFORMSKIPAPAYMENT
END

PROCEDURE INITIALIZETERMS
 WHILELIMIT=100000
 FOR I=1 TO TERMSMAXLINES
  DO
   TERMS(I)=""
  END
END

PROCEDURE INITIALIZEDATA
 WHILELIMIT=100000
 FOR I=1 TO LOANSMAX
  DO
   LOANID(I)=""
   FOR J=0 TO INELIGIBLEREASONSMAX
    DO
     LOANINELIGIBLECODE(I,J)=FALSE
    END
  END
 
 FOR TEMPLOOP=1 TO WARNINGSMAX
  DO
   LOANWARNINGS(TEMPLOOP)=FALSE
   ACCTWARNINGS(TEMPLOOP)=FALSE
  END

 FOR TEMPLOOP=1 TO SERVICECODEMAX
  DO
   LOANSERVICECODE(TEMPLOOP)=FALSE
  END

 FOR TEMPLOOP=1 TO FEEAMOUNTSMAX
  DO
   FEEAMOUNTS(TEMPLOOP)=$0.00
  END

 FOR TEMPLOOP=1 TO TYPESMAX
  DO
   LOANTYPES(TEMPLOOP)=TRUE
   SHARETYPES(TEMPLOOP)=TRUE
  END

 FEEAMOUNTS(0)=FEEAMOUNT
 GRACEDAYS=0


 [Initialize the ineligible reason messages]
 INELIGIBLEREASONS(INELIGIBLECODELOANTYPE)=
  "Ineligible Loan Type"
 INELIGIBLEREASONS(INELIGIBLECODELOANWARNING)=
  "Loan Warning Found"
 INELIGIBLEREASONS(INELIGIBLECODEMAXSKIPS)=
  "Max Loan skips already used for last 12 months"
 INELIGIBLEREASONS(INELIGIBLECODETIMESKIP)=
  "Insufficient time since last skip"
 INELIGIBLEREASONS(INELIGIBLECODEACCTWARNING)=
  "Account Warning Found"
 INELIGIBLEREASONS(INELIGIBLECODESERVICECODE)=
  "Required Service Code(s) not found"
 INELIGIBLEREASONS(INELIGIBLECODEMINPAYMENT)=
  "Payment Less than Minimum allowed"
 INELIGIBLEREASONS(INELIGIBLECODEMAXPAYMENT)=
  "Payment More than Maximum allowed"
 INELIGIBLEREASONS(INELIGIBLECODETIMEOPEN)=
  "Insufficient time since loan was opened"
 INELIGIBLEREASONS(INELIGIBLECODEPASTDUE)=
  "Loan past due grace period elapsed"
END

PROCEDURE GETLOANDATA

 TOTALLOANS=0
 ELIGIBLELOANS=0
 FOR EACH LOAN WITH (LOAN:CLOSEDATE=DATENULL AND
                     LOAN:CHARGEOFFDATE=DATENULL AND
                     LOAN:BALANCE>$0.00)
  DO
   TOTALLOANS=TOTALLOANS+1
   FOR I=0 TO INELIGIBLEREASONSMAX
    DO
     LOANINELIGIBLECODE(TOTALLOANS,I)=FALSE
    END

   LOANID(TOTALLOANS)=LOAN:ID
   IF LOANTYPES(LOAN:TYPE)=FALSE THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODELOANTYPE)=TRUE
   FOR WARNINGLOOP=1 TO WARNINGSMAX
    DO
     IF ANYWARNING(ACCOUNT,WARNINGLOOP) AND
        ACCTWARNINGS(WARNINGLOOP)=TRUE THEN
      LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODEACCTWARNING)=TRUE
     IF ANYWARNING(LOAN,WARNINGLOOP) AND 
        LOANWARNINGS(WARNINGLOOP)=TRUE THEN
      LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODELOANWARNING)=TRUE
    END

   SERVICEFOUND=FALSE
   FOR SERVICELOOP=1 TO SERVICECODEMAX
    DO
     IF ANYSERVICE(LOAN,SERVICELOOP) AND
        LOANSERVICECODE(SERVICELOOP)=TRUE THEN
      SERVICEFOUND=TRUE
    END

[ If none are found, check to see if any are defined]
   IF SERVICEFOUND=FALSE THEN
    DO
     [default to none found]
     SERVICEFOUND=TRUE
     FOR I=0 TO SERVICECODEMAX
      DO
[ One found, loan ineligible]
       IF LOANSERVICECODE(I)=TRUE THEN
        SERVICEFOUND=FALSE
      END
    END
[ If none found, and some are defined]
   IF SERVICEFOUND=FALSE THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODESERVICECODE)=TRUE

[ Payment amount]
   IF LOANMINPAYMENT>$0.00 AND LOAN:PAYMENT<LOANMINPAYMENT THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODEMINPAYMENT)=TRUE
   IF LOANMAXPAYMENT>$0.00 AND LOAN:PAYMENT>LOANMAXPAYMENT THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODEMAXPAYMENT)=TRUE
[ Loan season]
   IF LOANSEASON>0 AND 
      SYSTEMDATE<DATEOFFSET(LOAN:OPENDATE,LOANSEASON,0) THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODETIMEOPEN)=TRUE
[ Grace period]
   IF LOAN:DUEDATE+GRACEDAYS<SYSTEMDATE THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODEPASTDUE)=TRUE
[ Skips]
   SKIPCOUNT=0
   FOR EACH LOAN TRACKING WITH (LOAN TRACKING:TYPE=@RGUSERNUM1 AND
                                LOAN TRACKING:USERDATE1>=
                                DATEOFFSET(SYSTEMDATE,-12,0))
    DO
     SKIPCOUNT=SKIPCOUNT+1
    END 
   UNTIL SKIPCOUNT=MAXSKIPS
[ Too many skips in last year]
   IF SKIPCOUNT>=MAXSKIPS THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODEMAXSKIPS)=TRUE

   SKIPCOUNT=0
   FOR EACH LOAN TRACKING WITH (
                     LOAN TRACKING:TYPE=@RGUSERNUM1 AND
                     LOAN TRACKING:USERDATE2>=
                     DATEOFFSET(LOAN:DUEDATE,-MINMONTHSINCE,0))
    DO
     SKIPCOUNT=SKIPCOUNT+1
    END 
   UNTIL SKIPCOUNT=1
[ Too soon since last skip]
   IF SKIPCOUNT>0 THEN
    LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODETIMESKIP)=TRUE

[ If any codes are set, loan is ineligible]
   FOR I=1 TO INELIGIBLEREASONSMAX
    DO
     IF LOANINELIGIBLECODE(TOTALLOANS,I)=TRUE THEN
      LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODESET)=TRUE
    END

[ Increment eligible loans]
   IF LOANINELIGIBLECODE(TOTALLOANS,INELIGIBLECODESET)=FALSE THEN
    ELIGIBLELOANS=ELIGIBLELOANS+1
  END 
 UNTIL TOTALLOANS=LOANSMAX  [FOR EACH LOAN]
END

PROCEDURE GETSKIPCRITERIA

 LDUEDATE=DATENULL
 LNEWDUEDATE=DATENULL
 LPAYMENT=$0.00
 LMATURITYDATE=DATENULL
 LNEWMATURITYDATE=DATENULL
 LDESCRIPTION=""

 FOR EACH LOAN WITH LOAN:ID=LID
  DO
   LDUEDATE=LOAN:DUEDATE
   LPAYMENT=LOAN:PAYMENT
   LMATURITYDATE=LOAN:MATURITYDATE
   LDESCRIPTION=LOAN:DESCRIPTION
   IF (LOAN:DUEDAY2<>0 AND
       DAY(LOAN:DUEDATE)>LOAN:DUEDAY1) THEN
    DO
     LNEWDUEDATE=DATEOFFSET(LOAN:DUEDATE,1,LOAN:DUEDAY2)
     LNEWMATURITYDATE=DATEOFFSET(LOAN:MATURITYDATE,1,LOAN:DUEDAY2)
    END
   ELSE
    DO
     LNEWDUEDATE=DATEOFFSET(LOAN:DUEDATE,1,LOAN:DUEDAY1)
     LNEWMATURITYDATE=DATEOFFSET(LOAN:MATURITYDATE,1,LOAN:DUEDAY1)
    END
  END
END
