[
Filename:          BANNO.RDC.ELIGIBILITY

Summary:           This PowerOn reads from the BANNO.RDC.CONFIG Help File to produce
                   eligibility output for Banno to process
                   
                   

Type:              On-Demand Specfile

Created:           06/13/2019
Created By:        Gary Schultz
Created For:       325 - Embers Credit Union

Released:          

Programming Time:  

Last Review Date:  
Last Reviewed By:  

Last Revised Date: 
Revised By:        

Changelog:         
====================================================================================
USER NAME        DATE         DESCRIPTION (INCLUDING TKT #)
------------  -----------     ------------------------------------------------------
gschultz      06/13/2019      187955 - repgen created from template
CSimms, Banno 08/15/2019      adjusted arrays, moved the get share info, excluded
                              CDs & clubs, adjusted the SymX output
csimms        10/17/2019      switched from help file to letterfile

Comments:
]


SYMCONNECT
STATELESS
TARGET = ACCOUNT

DEFINE
 #INCLUDE "RB.LISTEXPAND.DEF"

 FILENAME  = CHARACTER
 FILENUM   = NUMBER
 FILETEXT  = CHARACTER
 FILEERROR = CHARACTER

 FIELDNAME  = CHARACTER
 FIELDVALUE = CHARACTER

 TRKLEVEL     = NUMBER ARRAY(9)   [0-Account, 1-Share]
 TRKTYPE      = NUMBER
 TRKFIELDNUM  = NUMBER
 TRKMAP       = CHARACTER ARRAY(130)
 TRKFOUND     = NUMBER
 SID          = CHARACTER
 SHRTYPELIST  = NUMBER ARRAY(9999)
 SCOUNT       = NUMBER
 SHRID        = CHARACTER(4) ARRAY(9999)
 SHRDESC      = CHARACTER(40) ARRAY(9999)
 BANNOTYPE    = CHARACTER ARRAY(9999)

 EXCLWARNING  = NUMBER ARRAY(9999)
 VALIDSHARE   = NUMBER
 WARNINGFOUND = NUMBER
 FIRSTSHARE   = NUMBER

 eligibleShareTypes      = CHARACTER
 disableWarningCodes     = CHARACTER
 dailyDepositsCount      = CHARACTER
 dailyDepositsLimit      = CHARACTER
 dailyAmount             = CHARACTER
 dailyAmountLimit        = CHARACTER
 periodDepositsCount     = CHARACTER
 periodDepositsLimit     = CHARACTER
 periodAmount            = CHARACTER
 periodAmountLimit       = CHARACTER
 singleItemLimit         = CHARACTER

 TRKUSERFIELD = CHARACTER ARRAY(130)

 TUSERCHAR   = CHARACTER ARRAY(20,99)
 TUSERAMOUNT = MONEY ARRAY(20,99)
 TUSERNUMBER = NUMBER ARRAY(20,99)
 TUSERCODE   = NUMBER ARRAY(20,99)
 TUSERDATE   = DATE ARRAY(20,99)
 TUSERRATE   = RATE ARRAY(20,99)

 LEFTTAG = CHARACTER
 RIGHTTAG = CHARACTER

 TEMPLINE = CHARACTER
 FORMLOOP = NUMBER
 Q        = CHARACTER(1)
 I        = NUMBER
 TRUE     = 1
 FALSE    = 0
END


SETUP
 Q = CTRLCHR(34)
 CALL FILLTRACKINGMAP
 FILENAME = "BANNO.RDC.CONFIG"
 FILEOPEN("LETTER",FILENAME,"READ",FILENUM,FILEERROR)
 WHILE FILEERROR = ""
  DO
   FILEREADLINE(FILENUM,FILETEXT,FILEERROR)
   IF FILETEXT<>"" THEN
    DO
     CALL READFILE
     CALL PROCESSFILE
    END
  END
 FILECLOSE(FILENUM,FILEERROR)
END


SELECT
 NONE
END


PRINT TITLE = "Banno Eligibility"
 SCOUNT = 0
 VALIDSHARE = FALSE
 FOR I = 0 TO 9999
  DO
   WHILELIMIT = 100000
   IF SHRTYPELIST(I)=TRUE AND ANY SHARE WITH (SHARE:TYPE = I AND
                                             (SHARE:SHARECODE=0 OR
                                              SHARE:SHARECODE=1) AND 
                                              SHARE:CHARGEOFFDATE = '--/--/--' AND
                                              SHARE:CLOSEDATE = '--/--/--') THEN
     VALIDSHARE = TRUE
  END

 WARNINGFOUND = FALSE
 FOR I = 1 TO 999
  DO
   WHILELIMIT = 100000
   IF EXCLWARNING(I)=TRUE AND (ANYWARNING(ACCOUNT,I) OR
                               ANYWARNING(SHARE,I) OR
                               ANYWARNING(LOAN,I)) THEN
     WARNINGFOUND = TRUE
  END

 IF VALIDSHARE=TRUE AND WARNINGFOUND = FALSE THEN
  DO
   FOR EACH SHARE WITH ( SHARE:CLOSEDATE = '--/--/--' AND
                         SHARE:CHARGEOFFDATE = '--/--/--' AND
                        (SHARE:SHARECODE=0 OR
                         SHARE:SHARECODE=1) AND 
                         SHRTYPELIST(SHARE:TYPE) = TRUE )
    DO
     SCOUNT = SCOUNT + 1
     SHRID(SCOUNT)   = SHARE:ID
     SHRDESC(SCOUNT) = SHARE:DESCRIPTION
     IF SHARE:SHARECODE=0 THEN
      BANNOTYPE(SCOUNT) = "SAV"
     ELSE IF SHARE:SHARECODE=1 THEN
      BANNOTYPE(SCOUNT) = "DDA"
    END
  END

 CALL DISPLAYOUTPUT
END


PROCEDURE DISPLAYHTMLOUTPUT  [used for troubleshooting so it can be ran from Episys]
 LEFTTAG = "&lt;"
 RIGHTTAG = "&gt;"
 HTMLVIEWOPEN
 HTMLVIEWLINE("<HTML>")
 HTMLVIEWLINE("<HEAD>")
 HTMLVIEWLINE("<link href='https://fonts.googleapis.com/css?family=Open+Sans&display=swap' rel='stylesheet'>")
 HTMLVIEWLINE("<style>")
 HTMLVIEWLINE(" body {")
 HTMLVIEWLINE(" font-family: 'Courier New', 'Open Sans', sans-serif;")
 HTMLVIEWLINE("}")
 HTMLVIEWLINE("</style>")
 HTMLVIEWLINE("</HEAD>")
 HTMLVIEWLINE("<BODY>")

 HTMLVIEWLINE("{<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"Shares"+Q+": [<BR>")
 IF SCOUNT > 1 THEN
  DO
   FOR I = 1 TO SCOUNT-1
    DO
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;{<BR>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"AccountNum"+Q+": "+Q+ACCOUNT:NUMBER+Q+",<BR>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"ID"+Q+": "+Q+SHRID(I)+Q+",<BR>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"TYPE"+Q+": "+Q+BANNOTYPE(I)+Q+",<BR>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"Description"+Q+": "+Q+SHRDESC(I)+Q+"<BR>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;},<BR>")
   END
  END

 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;{<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"AccountNum"+Q+": "+Q+ACCOUNT:NUMBER+Q+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"ID"+Q+": "+Q+SHRID(SCOUNT)+Q+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"TYPE"+Q+": "+Q+BANNOTYPE(SCOUNT)+Q+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"Description"+Q+": "+Q+SHRDESC(SCOUNT)+Q+"<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;}<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;],<BR>")

 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"dailyDepositsCount"+Q+": "+dailyDepositsCount+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"dailyDepositsLimit"+Q+": "+dailyDepositsLimit+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"dailyAmount"+Q+": "+dailyAmount+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"dailyAmountLimit"+Q+": "+dailyAmountLimit+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"periodDepositsCount"+Q+": "+periodDepositsCount+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"periodDepositsLimit"+Q+": "+periodDepositsLimit+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"periodAmount"+Q+": "+periodAmount+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"periodAmountLimit"+Q+": "+periodAmountLimit+",<BR>")
 HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;"+Q+"singleItemLimit"+Q+": "+singleItemLimit+"<BR>")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE("</BODY>")
 HTMLVIEWLINE("</HTML>")
 HTMLVIEWDISPLAY
END


PROCEDURE DISPLAYOUTPUT
 IF TRKFOUND=FALSE THEN
  DO
   PRINT "ERROR: NO TRACKING FOUND"
   NEWLINE
  END

 ELSE
  DO
   PRINT "{"                                                             NEWLINE
   PRINT "    "+Q+"Shares"+Q+": ["                                       NEWLINE
   IF SCOUNT > 1 THEN
    DO
     FOR I = 1 TO SCOUNT-1
      DO
       IF SHRID(SCOUNT)<>"" THEN
        DO
         PRINT "        {"                                                   NEWLINE
         PRINT "            "+Q+"AccountNum"+Q+": "+Q+ACCOUNT:NUMBER+Q+","   NEWLINE
         PRINT "            "+Q+"ID"+Q+": "+Q+SHRID(I)+Q+","                 NEWLINE
         PRINT "            "+Q+"TYPE"+Q+": "+Q+BANNOTYPE(I)+Q+","           NEWLINE
         PRINT "            "+Q+"Description"+Q+": "+Q+SHRDESC(I)+Q          NEWLINE
         PRINT "        },"                                                  NEWLINE
        END
      END
    END
   IF SHRID(SCOUNT)<>"" THEN
    DO
     PRINT "        {"                                                       NEWLINE
     PRINT "            "+Q+"AccountNum"+Q+": "+Q+ACCOUNT:NUMBER+Q+","       NEWLINE
     PRINT "            "+Q+"ID"+Q+": "+Q
     IF LENGTH(SHRID(SCOUNT))=2 THEN
      PRINT "00"+SHRID(SCOUNT)
     IF LENGTH(SHRID(SCOUNT))=3 THEN
      PRINT "0"+SHRID(SCOUNT)
     ELSE PRINT SHRID(SCOUNT)
     PRINT Q+","                NEWLINE
     PRINT "            "+Q+"TYPE"+Q+": "+Q+BANNOTYPE(SCOUNT)+Q+","          NEWLINE
     PRINT "            "+Q+"Description"+Q+": "+Q+SHRDESC(SCOUNT)+Q         NEWLINE
     PRINT "        }"                                                       NEWLINE
    END

   PRINT "    ],"                                                          NEWLINE

 IF WARNINGFOUND=TRUE THEN
  DO
   PRINT "            "+Q+"ERROR"+Q+": "+Q+"WARNING ON ACCOUNT"+Q+","        NEWLINE
  END


   PRINT "    "+Q+"dailyDepositsCount"+Q+": "+dailyDepositsCount+","       NEWLINE   [NUMBER]
   PRINT "    "+Q+"dailyDepositsLimit"+Q+": "+dailyDepositsLimit+","       NEWLINE   [NUMBER]

   IF dailyAmount<>"null" THEN  [CMS IN HERE]
    DO
     PRINT "    "+Q+"dailyAmount"+Q+": "+Q+dailyAmount+Q+","               NEWLINE   [AMOUNT]
    END
   ELSE
    DO
     PRINT "    "+Q+"dailyAmount"+Q+": "+dailyAmount+","                   NEWLINE   [AMOUNT]
    END
 
   IF dailyAmountLimit<>"null" THEN
    DO 
     PRINT "    "+Q+"dailyAmountLimit"+Q+": "+Q+dailyAmountLimit+Q+","       NEWLINE   [AMOUNT]
    END
   ELSE
    DO
     PRINT "    "+Q+"dailyAmountLimit"+Q+": "+dailyAmountLimit+","       NEWLINE   [AMOUNT]
    END

   PRINT "    "+Q+"periodDepositsCount"+Q+": "+periodDepositsCount+","     NEWLINE   [NUMBER]
   PRINT "    "+Q+"periodDepositsLimit"+Q+": "+periodDepositsLimit+","     NEWLINE   [NUMBER]

   IF periodAmount<>"null" THEN
    DO 
     PRINT "    "+Q+"periodAmount"+Q+": "+Q+periodAmount+Q+","              NEWLINE   [AMOUNT]
    END
   ELSE 
    DO
     PRINT "    "+Q+"periodAmount"+Q+": "+periodAmount+","                 NEWLINE   [AMOUNT]
    END

   IF periodAmountLimit<>"null" THEN
    DO 
     PRINT "    "+Q+"periodAmountLimit"+Q+": "+Q+periodAmountLimit+Q+","     NEWLINE   [AMOUNT]
    END
   ELSE 
    DO
     PRINT "    "+Q+"periodAmountLimit"+Q+": "+periodAmountLimit+","     NEWLINE   [AMOUNT]
    END

   IF singleItemLimit<>"null" THEN
    DO
     PRINT "    "+Q+"singleItemLimit"+Q+": "+Q+singleItemLimit+Q             NEWLINE   [AMOUNT]
    END
   ELSE 
    DO
     PRINT "    "+Q+"singleItemLimit"+Q+": "+singleItemLimit              NEWLINE   [AMOUNT]
    END

   PRINT "}"                                                               NEWLINE
  END



END


PROCEDURE READFILE
 TEMPLINE = SEGMENT(FILETEXT,1,40)
 CALL REMOVETRAILINGSPACES
 FIELDNAME = TEMPLINE
 FIELDVALUE = SEGMENT(FILETEXT,41,LENGTH(FILETEXT))
END


PROCEDURE PROCESSFILE
 IF FIELDNAME="eligibilityTrackingType" THEN
  DO
   IF UPPERCASE(FIELDVALUE)="ACCOUNT" THEN
     TRKLEVEL(0) = TRUE
   ELSE IF UPPERCASE(FIELDVALUE)="SHARE" THEN
     TRKLEVEL(1) = TRUE
  END
 ELSE IF FIELDNAME="eligibilityTrackingNum" THEN
  DO
   TRKTYPE = NUMBER(VALUE(FIELDVALUE))
   CALL FILLTRACKINGARRAYS
  END
 ELSE IF FIELDNAME="eligibleShareTypes" THEN
  DO
   LELISTINPUT=FIELDVALUE
   CALL LISTEXPAND
   FOR I=0 TO 9999
    DO
     SHRTYPELIST(I)=LELIST(I)
    END
  END
 ELSE IF FIELDNAME="disableWarningCodes" THEN
  DO
   LELISTINPUT=FIELDVALUE
   CALL LISTEXPAND
   FOR I=0 TO 9999
    DO
     EXCLWARNING(I)=LELIST(I)
    END
  END
 ELSE IF FIELDNAME="dailyDepositsCount" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     dailyDepositsCount = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     dailyDepositsCount = "null"
  END
 ELSE IF FIELDNAME="dailyDepositsLimit" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     dailyDepositsLimit = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     dailyDepositsLimit = "null"
  END
 ELSE IF FIELDNAME="dailyAmount" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     dailyAmount = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     dailyAmount = "null"
  END
 ELSE IF FIELDNAME="dailyAmountLimit" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     dailyAmountLimit = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     dailyAmountLimit = "null"
  END
 ELSE IF FIELDNAME="periodDepositsCount" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     periodDepositsCount = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     periodDepositsCount = "null"
  END
 ELSE IF FIELDNAME="periodDepositsLimit" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     periodDepositsLimit = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     periodDepositsLimit = "null"
  END
 ELSE IF FIELDNAME="periodAmount" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     periodAmount = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     periodAmount = "null"
  END
 ELSE IF FIELDNAME="periodAmountLimit" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     periodAmountLimit = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     periodAmountLimit = "null"
  END
 ELSE IF FIELDNAME="singleItemLimit" THEN
  DO
   IF LENGTH(FIELDVALUE)>0 THEN
    DO
     CALL FINDFIELDNUM
     singleItemLimit = TRKUSERFIELD(TRKFIELDNUM)
    END
   ELSE
     singleItemLimit = "null"
  END
END


PROCEDURE FINDFIELDNUM
 TRKFIELDNUM=0
 FOR I = 10 TO 130
  DO
   IF FIELDVALUE<>"" AND UPPERCASE(FIELDVALUE)=TRKMAP(I) THEN
     TRKFIELDNUM = I
  END
END


PROCEDURE FILLTRACKINGARRAYS
 IF TRKLEVEL(0)=TRUE THEN
  DO
   IF ANY TRACKING WITH (TRACKING:TYPE = TRKTYPE AND
                        (TRACKING:EXPIREDATE = '--/--/--' OR
                         TRACKING:EXPIREDATE > SYSTEMDATE)) THEN
    DO
     TRKFOUND=TRUE
     FOR EACH TRACKING WITH (TRACKING:TYPE = TRKTYPE AND
                            (TRACKING:EXPIREDATE = '--/--/--' OR
                             TRACKING:EXPIREDATE > SYSTEMDATE) )
      DO
       TRKUSERFIELD(10) = FORMAT("########9",TRACKING:USERNUMBER1)     TRKUSERFIELD(71) = FORMAT("########9",TRACKING:USERNUMBER11)
       TRKUSERFIELD(11) = FORMAT("########9",TRACKING:USERNUMBER2)     TRKUSERFIELD(72) = FORMAT("########9",TRACKING:USERNUMBER12)
       TRKUSERFIELD(12) = FORMAT("########9",TRACKING:USERNUMBER3)     TRKUSERFIELD(73) = FORMAT("########9",TRACKING:USERNUMBER13)
       TRKUSERFIELD(13) = FORMAT("########9",TRACKING:USERNUMBER4)     TRKUSERFIELD(74) = FORMAT("########9",TRACKING:USERNUMBER14)
       TRKUSERFIELD(14) = FORMAT("########9",TRACKING:USERNUMBER5)     TRKUSERFIELD(75) = FORMAT("########9",TRACKING:USERNUMBER15)
       TRKUSERFIELD(15) = FORMAT("########9",TRACKING:USERNUMBER6)     TRKUSERFIELD(76) = FORMAT("########9",TRACKING:USERNUMBER16)
       TRKUSERFIELD(16) = FORMAT("########9",TRACKING:USERNUMBER7)     TRKUSERFIELD(77) = FORMAT("########9",TRACKING:USERNUMBER17)
       TRKUSERFIELD(17) = FORMAT("########9",TRACKING:USERNUMBER8)     TRKUSERFIELD(78) = FORMAT("########9",TRACKING:USERNUMBER18)
       TRKUSERFIELD(18) = FORMAT("########9",TRACKING:USERNUMBER9)     TRKUSERFIELD(79) = FORMAT("########9",TRACKING:USERNUMBER19)
       TRKUSERFIELD(19) = FORMAT("########9",TRACKING:USERNUMBER10)    TRKUSERFIELD(80) = FORMAT("########9",TRACKING:USERNUMBER20)

       TRKUSERFIELD(20) = TRACKING:USERCHAR1     TRKUSERFIELD(81) = TRACKING:USERCHAR11
       TRKUSERFIELD(21) = TRACKING:USERCHAR2     TRKUSERFIELD(82) = TRACKING:USERCHAR12
       TRKUSERFIELD(22) = TRACKING:USERCHAR3     TRKUSERFIELD(83) = TRACKING:USERCHAR13
       TRKUSERFIELD(23) = TRACKING:USERCHAR4     TRKUSERFIELD(84) = TRACKING:USERCHAR14
       TRKUSERFIELD(24) = TRACKING:USERCHAR5     TRKUSERFIELD(85) = TRACKING:USERCHAR15
       TRKUSERFIELD(25) = TRACKING:USERCHAR6     TRKUSERFIELD(86) = TRACKING:USERCHAR16
       TRKUSERFIELD(26) = TRACKING:USERCHAR7     TRKUSERFIELD(87) = TRACKING:USERCHAR17
       TRKUSERFIELD(27) = TRACKING:USERCHAR8     TRKUSERFIELD(88) = TRACKING:USERCHAR18
       TRKUSERFIELD(28) = TRACKING:USERCHAR9     TRKUSERFIELD(89) = TRACKING:USERCHAR19
       TRKUSERFIELD(29) = TRACKING:USERCHAR10    TRKUSERFIELD(90) = TRACKING:USERCHAR20

       TRKUSERFIELD(30) = FORMAT("########9.99",TRACKING:USERAMOUNT1)     TRKUSERFIELD(91)  = FORMAT("########9.99",TRACKING:USERAMOUNT11)
       TRKUSERFIELD(31) = FORMAT("########9.99",TRACKING:USERAMOUNT2)     TRKUSERFIELD(92)  = FORMAT("########9.99",TRACKING:USERAMOUNT12)
       TRKUSERFIELD(32) = FORMAT("########9.99",TRACKING:USERAMOUNT3)     TRKUSERFIELD(93)  = FORMAT("########9.99",TRACKING:USERAMOUNT13)
       TRKUSERFIELD(33) = FORMAT("########9.99",TRACKING:USERAMOUNT4)     TRKUSERFIELD(94)  = FORMAT("########9.99",TRACKING:USERAMOUNT14)
       TRKUSERFIELD(34) = FORMAT("########9.99",TRACKING:USERAMOUNT5)     TRKUSERFIELD(95)  = FORMAT("########9.99",TRACKING:USERAMOUNT15)
       TRKUSERFIELD(35) = FORMAT("########9.99",TRACKING:USERAMOUNT6)     TRKUSERFIELD(96)  = FORMAT("########9.99",TRACKING:USERAMOUNT16)
       TRKUSERFIELD(36) = FORMAT("########9.99",TRACKING:USERAMOUNT7)     TRKUSERFIELD(97)  = FORMAT("########9.99",TRACKING:USERAMOUNT17)
       TRKUSERFIELD(37) = FORMAT("########9.99",TRACKING:USERAMOUNT8)     TRKUSERFIELD(98)  = FORMAT("########9.99",TRACKING:USERAMOUNT18)
       TRKUSERFIELD(38) = FORMAT("########9.99",TRACKING:USERAMOUNT9)     TRKUSERFIELD(99)  = FORMAT("########9.99",TRACKING:USERAMOUNT19)
       TRKUSERFIELD(39) = FORMAT("########9.99",TRACKING:USERAMOUNT10)    TRKUSERFIELD(100) = FORMAT("########9.99",TRACKING:USERAMOUNT20)

       TRKUSERFIELD(40) = FORMAT("########9",TRACKING:USERCODE1)     TRKUSERFIELD(101) = FORMAT("########9",TRACKING:USERCODE11)
       TRKUSERFIELD(41) = FORMAT("########9",TRACKING:USERCODE2)     TRKUSERFIELD(102) = FORMAT("########9",TRACKING:USERCODE12)
       TRKUSERFIELD(42) = FORMAT("########9",TRACKING:USERCODE3)     TRKUSERFIELD(103) = FORMAT("########9",TRACKING:USERCODE13)
       TRKUSERFIELD(43) = FORMAT("########9",TRACKING:USERCODE4)     TRKUSERFIELD(104) = FORMAT("########9",TRACKING:USERCODE14)
       TRKUSERFIELD(44) = FORMAT("########9",TRACKING:USERCODE5)     TRKUSERFIELD(105) = FORMAT("########9",TRACKING:USERCODE15)
       TRKUSERFIELD(45) = FORMAT("########9",TRACKING:USERCODE6)     TRKUSERFIELD(106) = FORMAT("########9",TRACKING:USERCODE16)
       TRKUSERFIELD(46) = FORMAT("########9",TRACKING:USERCODE7)     TRKUSERFIELD(107) = FORMAT("########9",TRACKING:USERCODE17)
       TRKUSERFIELD(47) = FORMAT("########9",TRACKING:USERCODE8)     TRKUSERFIELD(108) = FORMAT("########9",TRACKING:USERCODE18)
       TRKUSERFIELD(48) = FORMAT("########9",TRACKING:USERCODE9)     TRKUSERFIELD(109) = FORMAT("########9",TRACKING:USERCODE19)
       TRKUSERFIELD(49) = FORMAT("########9",TRACKING:USERCODE10)    TRKUSERFIELD(110) = FORMAT("########9",TRACKING:USERCODE20)

       TRKUSERFIELD(50) = FORMAT("99/99/9999",TRACKING:USERDATE1)     TRKUSERFIELD(111) = FORMAT("99/99/9999",TRACKING:USERDATE11)
       TRKUSERFIELD(51) = FORMAT("99/99/9999",TRACKING:USERDATE2)     TRKUSERFIELD(112) = FORMAT("99/99/9999",TRACKING:USERDATE12)
       TRKUSERFIELD(52) = FORMAT("99/99/9999",TRACKING:USERDATE3)     TRKUSERFIELD(113) = FORMAT("99/99/9999",TRACKING:USERDATE13)
       TRKUSERFIELD(53) = FORMAT("99/99/9999",TRACKING:USERDATE4)     TRKUSERFIELD(114) = FORMAT("99/99/9999",TRACKING:USERDATE14)
       TRKUSERFIELD(54) = FORMAT("99/99/9999",TRACKING:USERDATE5)     TRKUSERFIELD(115) = FORMAT("99/99/9999",TRACKING:USERDATE15)
       TRKUSERFIELD(55) = FORMAT("99/99/9999",TRACKING:USERDATE6)     TRKUSERFIELD(116) = FORMAT("99/99/9999",TRACKING:USERDATE16)
       TRKUSERFIELD(56) = FORMAT("99/99/9999",TRACKING:USERDATE7)     TRKUSERFIELD(117) = FORMAT("99/99/9999",TRACKING:USERDATE17)
       TRKUSERFIELD(57) = FORMAT("99/99/9999",TRACKING:USERDATE8)     TRKUSERFIELD(118) = FORMAT("99/99/9999",TRACKING:USERDATE18)
       TRKUSERFIELD(58) = FORMAT("99/99/9999",TRACKING:USERDATE9)     TRKUSERFIELD(119) = FORMAT("99/99/9999",TRACKING:USERDATE19)
       TRKUSERFIELD(59) = FORMAT("99/99/9999",TRACKING:USERDATE10)    TRKUSERFIELD(120) = FORMAT("99/99/9999",TRACKING:USERDATE20)

       TRKUSERFIELD(60) = FORMAT("##9.999",TRACKING:USERRATE1)     TRKUSERFIELD(121) = FORMAT("##9.999",TRACKING:USERRATE11)
       TRKUSERFIELD(61) = FORMAT("##9.999",TRACKING:USERRATE2)     TRKUSERFIELD(122) = FORMAT("##9.999",TRACKING:USERRATE12)
       TRKUSERFIELD(62) = FORMAT("##9.999",TRACKING:USERRATE3)     TRKUSERFIELD(123) = FORMAT("##9.999",TRACKING:USERRATE13)
       TRKUSERFIELD(63) = FORMAT("##9.999",TRACKING:USERRATE4)     TRKUSERFIELD(124) = FORMAT("##9.999",TRACKING:USERRATE14)
       TRKUSERFIELD(64) = FORMAT("##9.999",TRACKING:USERRATE5)     TRKUSERFIELD(125) = FORMAT("##9.999",TRACKING:USERRATE15)
       TRKUSERFIELD(65) = FORMAT("##9.999",TRACKING:USERRATE6)     TRKUSERFIELD(126) = FORMAT("##9.999",TRACKING:USERRATE16)
       TRKUSERFIELD(66) = FORMAT("##9.999",TRACKING:USERRATE7)     TRKUSERFIELD(127) = FORMAT("##9.999",TRACKING:USERRATE17)
       TRKUSERFIELD(67) = FORMAT("##9.999",TRACKING:USERRATE8)     TRKUSERFIELD(128) = FORMAT("##9.999",TRACKING:USERRATE18)
       TRKUSERFIELD(68) = FORMAT("##9.999",TRACKING:USERRATE9)     TRKUSERFIELD(129) = FORMAT("##9.999",TRACKING:USERRATE19)
       TRKUSERFIELD(69) = FORMAT("##9.999",TRACKING:USERRATE10)    TRKUSERFIELD(130) = FORMAT("##9.999",TRACKING:USERRATE20)
      END
    END
   ELSE IF NOT ANY TRACKING WITH (TRACKING:TYPE = TRKTYPE AND
                                 (TRACKING:EXPIREDATE = '--/--/--' OR
                                  TRACKING:EXPIREDATE > SYSTEMDATE)) THEN
    DO
     TRKFOUND=FALSE
    END
  END
 ELSE IF TRKLEVEL(1)=TRUE THEN
  DO
   FOR EACH SHARE WITH ( SHARE:ID = SID )
    DO
     IF ANY SHARE TRACKING WITH (SHARE TRACKING:TYPE = TRKTYPE AND
                                (SHARE TRACKING:EXPIREDATE = '--/--/--' OR
                                 SHARE TRACKING:EXPIREDATE > SYSTEMDATE)) THEN
      DO
       TRKFOUND=TRUE
       FOR EACH SHARE TRACKING WITH ( SHARE TRACKING:TYPE = TRKTYPE AND
                                    (SHARE TRACKING:EXPIREDATE = '--/--/--' OR
                                     SHARE TRACKING:EXPIREDATE > SYSTEMDATE) )
        DO
         TRKUSERFIELD(10) = FORMAT("########9",SHARE TRACKING:USERNUMBER1)     TRKUSERFIELD(71) = FORMAT("########9",SHARE TRACKING:USERNUMBER11)
         TRKUSERFIELD(11) = FORMAT("########9",SHARE TRACKING:USERNUMBER2)     TRKUSERFIELD(72) = FORMAT("########9",SHARE TRACKING:USERNUMBER12)
         TRKUSERFIELD(12) = FORMAT("########9",SHARE TRACKING:USERNUMBER3)     TRKUSERFIELD(73) = FORMAT("########9",SHARE TRACKING:USERNUMBER13)
         TRKUSERFIELD(13) = FORMAT("########9",SHARE TRACKING:USERNUMBER4)     TRKUSERFIELD(74) = FORMAT("########9",SHARE TRACKING:USERNUMBER14)
         TRKUSERFIELD(14) = FORMAT("########9",SHARE TRACKING:USERNUMBER5)     TRKUSERFIELD(75) = FORMAT("########9",SHARE TRACKING:USERNUMBER15)
         TRKUSERFIELD(15) = FORMAT("########9",SHARE TRACKING:USERNUMBER6)     TRKUSERFIELD(76) = FORMAT("########9",SHARE TRACKING:USERNUMBER16)
         TRKUSERFIELD(16) = FORMAT("########9",SHARE TRACKING:USERNUMBER7)     TRKUSERFIELD(77) = FORMAT("########9",SHARE TRACKING:USERNUMBER17)
         TRKUSERFIELD(17) = FORMAT("########9",SHARE TRACKING:USERNUMBER8)     TRKUSERFIELD(78) = FORMAT("########9",SHARE TRACKING:USERNUMBER18)
         TRKUSERFIELD(18) = FORMAT("########9",SHARE TRACKING:USERNUMBER9)     TRKUSERFIELD(79) = FORMAT("########9",SHARE TRACKING:USERNUMBER19)
         TRKUSERFIELD(19) = FORMAT("########9",SHARE TRACKING:USERNUMBER10)    TRKUSERFIELD(80) = FORMAT("########9",SHARE TRACKING:USERNUMBER20)

         TRKUSERFIELD(20) = SHARE TRACKING:USERCHAR1     TRKUSERFIELD(81) = SHARE TRACKING:USERCHAR11
         TRKUSERFIELD(21) = SHARE TRACKING:USERCHAR2     TRKUSERFIELD(82) = SHARE TRACKING:USERCHAR12
         TRKUSERFIELD(22) = SHARE TRACKING:USERCHAR3     TRKUSERFIELD(83) = SHARE TRACKING:USERCHAR13
         TRKUSERFIELD(23) = SHARE TRACKING:USERCHAR4     TRKUSERFIELD(84) = SHARE TRACKING:USERCHAR14
         TRKUSERFIELD(24) = SHARE TRACKING:USERCHAR5     TRKUSERFIELD(85) = SHARE TRACKING:USERCHAR15
         TRKUSERFIELD(25) = SHARE TRACKING:USERCHAR6     TRKUSERFIELD(86) = SHARE TRACKING:USERCHAR16
         TRKUSERFIELD(26) = SHARE TRACKING:USERCHAR7     TRKUSERFIELD(87) = SHARE TRACKING:USERCHAR17
         TRKUSERFIELD(27) = SHARE TRACKING:USERCHAR8     TRKUSERFIELD(88) = SHARE TRACKING:USERCHAR18
         TRKUSERFIELD(28) = SHARE TRACKING:USERCHAR9     TRKUSERFIELD(89) = SHARE TRACKING:USERCHAR19
         TRKUSERFIELD(29) = SHARE TRACKING:USERCHAR10    TRKUSERFIELD(90) = SHARE TRACKING:USERCHAR20

         TRKUSERFIELD(30) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT1)     TRKUSERFIELD(91)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT11)
         TRKUSERFIELD(31) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT2)     TRKUSERFIELD(92)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT12)
         TRKUSERFIELD(32) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT3)     TRKUSERFIELD(93)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT13)
         TRKUSERFIELD(33) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT4)     TRKUSERFIELD(94)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT14)
         TRKUSERFIELD(34) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT5)     TRKUSERFIELD(95)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT15)
         TRKUSERFIELD(35) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT6)     TRKUSERFIELD(96)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT16)
         TRKUSERFIELD(36) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT7)     TRKUSERFIELD(97)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT17)
         TRKUSERFIELD(37) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT8)     TRKUSERFIELD(98)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT18)
         TRKUSERFIELD(38) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT9)     TRKUSERFIELD(99)  = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT19)
         TRKUSERFIELD(39) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT10)    TRKUSERFIELD(100) = FORMAT("########9.99",SHARE TRACKING:USERAMOUNT20)

         TRKUSERFIELD(40) = FORMAT("########9",SHARE TRACKING:USERCODE1)     TRKUSERFIELD(101) = FORMAT("########9",SHARE TRACKING:USERCODE11)
         TRKUSERFIELD(41) = FORMAT("########9",SHARE TRACKING:USERCODE2)     TRKUSERFIELD(102) = FORMAT("########9",SHARE TRACKING:USERCODE12)
         TRKUSERFIELD(42) = FORMAT("########9",SHARE TRACKING:USERCODE3)     TRKUSERFIELD(103) = FORMAT("########9",SHARE TRACKING:USERCODE13)
         TRKUSERFIELD(43) = FORMAT("########9",SHARE TRACKING:USERCODE4)     TRKUSERFIELD(104) = FORMAT("########9",SHARE TRACKING:USERCODE14)
         TRKUSERFIELD(44) = FORMAT("########9",SHARE TRACKING:USERCODE5)     TRKUSERFIELD(105) = FORMAT("########9",SHARE TRACKING:USERCODE15)
         TRKUSERFIELD(45) = FORMAT("########9",SHARE TRACKING:USERCODE6)     TRKUSERFIELD(106) = FORMAT("########9",SHARE TRACKING:USERCODE16)
         TRKUSERFIELD(46) = FORMAT("########9",SHARE TRACKING:USERCODE7)     TRKUSERFIELD(107) = FORMAT("########9",SHARE TRACKING:USERCODE17)
         TRKUSERFIELD(47) = FORMAT("########9",SHARE TRACKING:USERCODE8)     TRKUSERFIELD(108) = FORMAT("########9",SHARE TRACKING:USERCODE18)
         TRKUSERFIELD(48) = FORMAT("########9",SHARE TRACKING:USERCODE9)     TRKUSERFIELD(109) = FORMAT("########9",SHARE TRACKING:USERCODE19)
         TRKUSERFIELD(49) = FORMAT("########9",SHARE TRACKING:USERCODE10)    TRKUSERFIELD(110) = FORMAT("########9",SHARE TRACKING:USERCODE20)

         TRKUSERFIELD(50) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE1)     TRKUSERFIELD(111) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE11)
         TRKUSERFIELD(51) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE2)     TRKUSERFIELD(112) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE12)
         TRKUSERFIELD(52) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE3)     TRKUSERFIELD(113) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE13)
         TRKUSERFIELD(53) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE4)     TRKUSERFIELD(114) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE14)
         TRKUSERFIELD(54) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE5)     TRKUSERFIELD(115) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE15)
         TRKUSERFIELD(55) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE6)     TRKUSERFIELD(116) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE16)
         TRKUSERFIELD(56) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE7)     TRKUSERFIELD(117) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE17)
         TRKUSERFIELD(57) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE8)     TRKUSERFIELD(118) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE18)
         TRKUSERFIELD(58) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE9)     TRKUSERFIELD(119) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE19)
         TRKUSERFIELD(59) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE10)    TRKUSERFIELD(120) = FORMAT("99/99/9999",SHARE TRACKING:USERDATE20)

         TRKUSERFIELD(60) = FORMAT("##9.999",SHARE TRACKING:USERRATE1)     TRKUSERFIELD(121) = FORMAT("##9.999",SHARE TRACKING:USERRATE11)
         TRKUSERFIELD(61) = FORMAT("##9.999",SHARE TRACKING:USERRATE2)     TRKUSERFIELD(122) = FORMAT("##9.999",SHARE TRACKING:USERRATE12)
         TRKUSERFIELD(62) = FORMAT("##9.999",SHARE TRACKING:USERRATE3)     TRKUSERFIELD(123) = FORMAT("##9.999",SHARE TRACKING:USERRATE13)
         TRKUSERFIELD(63) = FORMAT("##9.999",SHARE TRACKING:USERRATE4)     TRKUSERFIELD(124) = FORMAT("##9.999",SHARE TRACKING:USERRATE14)
         TRKUSERFIELD(64) = FORMAT("##9.999",SHARE TRACKING:USERRATE5)     TRKUSERFIELD(125) = FORMAT("##9.999",SHARE TRACKING:USERRATE15)
         TRKUSERFIELD(65) = FORMAT("##9.999",SHARE TRACKING:USERRATE6)     TRKUSERFIELD(126) = FORMAT("##9.999",SHARE TRACKING:USERRATE16)
         TRKUSERFIELD(66) = FORMAT("##9.999",SHARE TRACKING:USERRATE7)     TRKUSERFIELD(127) = FORMAT("##9.999",SHARE TRACKING:USERRATE17)
         TRKUSERFIELD(67) = FORMAT("##9.999",SHARE TRACKING:USERRATE8)     TRKUSERFIELD(128) = FORMAT("##9.999",SHARE TRACKING:USERRATE18)
         TRKUSERFIELD(68) = FORMAT("##9.999",SHARE TRACKING:USERRATE9)     TRKUSERFIELD(129) = FORMAT("##9.999",SHARE TRACKING:USERRATE19)
         TRKUSERFIELD(69) = FORMAT("##9.999",SHARE TRACKING:USERRATE10)    TRKUSERFIELD(130) = FORMAT("##9.999",SHARE TRACKING:USERRATE20)
        END
      END
     IF NOT ANY SHARE TRACKING WITH (SHARE TRACKING:TYPE = TRKTYPE AND
                                    (SHARE TRACKING:EXPIREDATE = '--/--/--' OR
                                     SHARE TRACKING:EXPIREDATE > SYSTEMDATE)) THEN
      DO
       TRKFOUND=FALSE
      END
    END
  END

 FOR I = 10 TO 130
  DO
   TEMPLINE = TRKUSERFIELD(I)
   CALL REMOVELEADINGSPACES
   TRKUSERFIELD(I) = TEMPLINE
  END
END


PROCEDURE FILLTRACKINGMAP
 TRKMAP(010)="USERNUMBER1"
 TRKMAP(011)="USERNUMBER2"
 TRKMAP(012)="USERNUMBER3"
 TRKMAP(013)="USERNUMBER4"
 TRKMAP(014)="USERNUMBER5"
 TRKMAP(015)="USERNUMBER6"
 TRKMAP(016)="USERNUMBER7"
 TRKMAP(017)="USERNUMBER8"
 TRKMAP(018)="USERNUMBER9"
 TRKMAP(019)="USERNUMBER10"
 TRKMAP(071)="USERNUMBER11"
 TRKMAP(072)="USERNUMBER12"
 TRKMAP(073)="USERNUMBER13"
 TRKMAP(074)="USERNUMBER14"
 TRKMAP(075)="USERNUMBER15"
 TRKMAP(076)="USERNUMBER16"
 TRKMAP(077)="USERNUMBER17"
 TRKMAP(078)="USERNUMBER18"
 TRKMAP(079)="USERNUMBER19"
 TRKMAP(080)="USERNUMBER20"

 TRKMAP(020)="USERCHAR1"
 TRKMAP(021)="USERCHAR2"
 TRKMAP(022)="USERCHAR3"
 TRKMAP(023)="USERCHAR4"
 TRKMAP(024)="USERCHAR5"
 TRKMAP(025)="USERCHAR6"
 TRKMAP(026)="USERCHAR7"
 TRKMAP(027)="USERCHAR8"
 TRKMAP(028)="USERCHAR9"
 TRKMAP(029)="USERCHAR10"
 TRKMAP(081)="USERCHAR11"
 TRKMAP(082)="USERCHAR12"
 TRKMAP(083)="USERCHAR13"
 TRKMAP(084)="USERCHAR14"
 TRKMAP(085)="USERCHAR15"
 TRKMAP(086)="USERCHAR16"
 TRKMAP(087)="USERCHAR17"
 TRKMAP(088)="USERCHAR18"
 TRKMAP(089)="USERCHAR19"
 TRKMAP(090)="USERCHAR20"

 TRKMAP(030)="USERAMOUNT1"
 TRKMAP(031)="USERAMOUNT2"
 TRKMAP(032)="USERAMOUNT3"
 TRKMAP(033)="USERAMOUNT4"
 TRKMAP(034)="USERAMOUNT5"
 TRKMAP(035)="USERAMOUNT6"
 TRKMAP(036)="USERAMOUNT7"
 TRKMAP(037)="USERAMOUNT8"
 TRKMAP(038)="USERAMOUNT9"
 TRKMAP(039)="USERAMOUNT10"
 TRKMAP(091)="USERAMOUNT11"
 TRKMAP(092)="USERAMOUNT12"
 TRKMAP(093)="USERAMOUNT13"
 TRKMAP(094)="USERAMOUNT14"
 TRKMAP(095)="USERAMOUNT15"
 TRKMAP(096)="USERAMOUNT16"
 TRKMAP(097)="USERAMOUNT17"
 TRKMAP(098)="USERAMOUNT18"
 TRKMAP(099)="USERAMOUNT19"
 TRKMAP(100)="USERAMOUNT20"

 TRKMAP(040)="USERCODE1"
 TRKMAP(041)="USERCODE2"
 TRKMAP(042)="USERCODE3"
 TRKMAP(043)="USERCODE4"
 TRKMAP(044)="USERCODE5"
 TRKMAP(045)="USERCODE6"
 TRKMAP(046)="USERCODE7"
 TRKMAP(047)="USERCODE8"
 TRKMAP(048)="USERCODE9"
 TRKMAP(049)="USERCODE10"
 TRKMAP(101)="USERCODE11"
 TRKMAP(102)="USERCODE12"
 TRKMAP(103)="USERCODE13"
 TRKMAP(104)="USERCODE14"
 TRKMAP(105)="USERCODE15"
 TRKMAP(106)="USERCODE16"
 TRKMAP(107)="USERCODE17"
 TRKMAP(108)="USERCODE18"
 TRKMAP(109)="USERCODE19"
 TRKMAP(110)="USERCODE20"

 TRKMAP(050)="USERDATE1"
 TRKMAP(051)="USERDATE2"
 TRKMAP(052)="USERDATE3"
 TRKMAP(053)="USERDATE4"
 TRKMAP(054)="USERDATE5"
 TRKMAP(055)="USERDATE6"
 TRKMAP(056)="USERDATE7"
 TRKMAP(057)="USERDATE8"
 TRKMAP(058)="USERDATE9"
 TRKMAP(059)="USERDATE10"
 TRKMAP(111)="USERDATE11"
 TRKMAP(112)="USERDATE12"
 TRKMAP(113)="USERDATE13"
 TRKMAP(114)="USERDATE14"
 TRKMAP(115)="USERDATE15"
 TRKMAP(116)="USERDATE16"
 TRKMAP(117)="USERDATE17"
 TRKMAP(118)="USERDATE18"
 TRKMAP(119)="USERDATE19"
 TRKMAP(120)="USERDATE20"

 TRKMAP(060)="USERRATE1"
 TRKMAP(061)="USERRATE2"
 TRKMAP(062)="USERRATE3"
 TRKMAP(063)="USERRATE4"
 TRKMAP(064)="USERRATE5"
 TRKMAP(065)="USERRATE6"
 TRKMAP(066)="USERRATE7"
 TRKMAP(067)="USERRATE8"
 TRKMAP(068)="USERRATE9"
 TRKMAP(069)="USERRATE10"
 TRKMAP(121)="USERRATE11"
 TRKMAP(122)="USERRATE12"
 TRKMAP(123)="USERRATE13"
 TRKMAP(124)="USERRATE14"
 TRKMAP(125)="USERRATE15"
 TRKMAP(126)="USERRATE16"
 TRKMAP(127)="USERRATE17"
 TRKMAP(128)="USERRATE18"
 TRKMAP(129)="USERRATE19"
 TRKMAP(130)="USERRATE20"
END


PROCEDURE BLANKVALUES
 dailyDepositsCount  = "null"
 dailyDepositsLimit  = "null"
 dailyAmount         = "null"
 dailyAmountLimit    = "null"
 periodDepositsCount = "null"
 periodDepositsLimit = "null"
 periodAmount        = "null"
 periodAmountLimit   = "null"
 singleItemLimit     = "null"
END


PROCEDURE REMOVETRAILINGSPACES
  FORMLOOP = LENGTH(TEMPLINE)
  WHILE (SEGMENT(TEMPLINE,FORMLOOP,FORMLOOP)=" " OR
         SEGMENT(TEMPLINE,FORMLOOP,FORMLOOP)="_") AND
         FORMLOOP>0
   DO
    FORMLOOP = FORMLOOP -1
   END
  TEMPLINE = SEGMENT(TEMPLINE,1,FORMLOOP)
END


PROCEDURE REMOVELEADINGSPACES
  FORMLOOP = 1
  WHILE SEGMENT(TEMPLINE,FORMLOOP,FORMLOOP)="_" OR
        SEGMENT(TEMPLINE,FORMLOOP,FORMLOOP)=" " AND
        FORMLOOP<LENGTH(TEMPLINE)
   DO
    FORMLOOP = FORMLOOP + 1
   END
  TEMPLINE = SEGMENT(TEMPLINE,FORMLOOP,LENGTH(TEMPLINE))
END


#INCLUDE "RB.LISTEXPAND"
